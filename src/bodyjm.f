      SUBROUTINE BODYJM
C
C***  COMPUTE BODY AERODYNAMICS USING JORGENSEN'S METHOD
C
      COMMON /IBODY/  PBODY,  BODY(400)
      COMMON /BODYI/  XNX, X(20), S(20), P(20), R(20), ZU(20),
     1                ZL(20), BNOSE, BTAIL, BLN, BLA, DS,
     2                ITYPE,METHOD,ELLIP
      COMMON /FLGTCD/ FLC(160)
      COMMON /OPTION/ SREF, CBARR, ROUGFC, BLREF
      COMMON /SYNTSS/ XCG, XW, ZW, ALIW, ZCG, XH, ZH, ALIH, XV,
     1                VERTUP, HINAX, XVF, SCALE, ZV, ZVF, YV, YF,
     2                PHIV, PHIF
      LOGICAL VERTUP
      COMMON /BDATA/  BD(762)
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD,KAND
      COMMON /OVERLY/ NLOG,NMACH,II,NALPHA,IG,NF,LF,K
C
      DIMENSION CD(20), CL(20), CM(20), CN(20), CA(20), CDC(20)
      DIMENSION RX(20)
      DIMENSION REQ(20),CNVIS(20),CNPOT(20),CMVIS(20),CMPOT(20)
      EQUIVALENCE (CD(1),BODY(1 )), (CL(1),BODY(21)), (CM(1),BODY(41))
      EQUIVALENCE (CN(1),BODY(61)), (CA(1),BODY(81)), (CDC(1),BD(135))
      EQUIVALENCE (SB,BD(57)), (SP,BD(275)), (ETA,BD(76)), (LB,BD(1))
      EQUIVALENCE (CD0,BD(61))
      EQUIVALENCE (VB,BD(34)),(XC,BD(35))
      EQUIVALENCE (REQ(1),BD(536)),(CNPOT(1),BD(556)),
     1 (CNVIS(1),BD(576)),(CMPOT(1),BD(596)),(CMVIS(1),BD(616))
C
      REAL LB, METHOD, ITYPE
C
C***  CALCULATE VB AND XC
C
      NX = XNX+0.5
      DO 1000 I=1,NX
         REQ(I)=SQRT(S(I)/PI)
         RX(I)=REQ(I)*X(I)
 1000 CONTINUE
      CALL TRAPZ(REQ,NX,X,VB,-1)
      CALL TRAPZ(RX ,NX,X,XC, 1)
      CALL TRAPZ(REQ,NX,X,SP, 1)
      SP = 2.0*SP
      XC = 2.0*XC/SP
C
C***  CALCULATE CN, CM, CA, CL, AND CD
C
      A1 = (VB-SB*(LB-XCG))/(SREF*CBARR)
      A2 = (SP*(XCG-XC))/(SREF*CBARR)
      A3 = SB
      IF(ELLIP .LE. UNUSED) ELLIP = 1.0
      IF(ELLIP .EQ. 1.0) A1 = BODY(121)*RAD/2.0
      IF(ELLIP .EQ. 1.0) A3 = BODY(101)*SREF*RAD/2.0
      DO 1010 I=1,NALPHA
        ALP = FLC(I+22)/RAD
        SA  = SIN(ALP)
        CS  = COS(ALP)
        FA  = SIN(2.0*ALP)*COS(ALP/2.0)
        IF(ELLIP.LT.1.)AOB=1./ELLIP
        IF(ELLIP.GE.1.)AOB=ELLIP
        IF(ELLIP.GE.1.)CNOCNS=1./AOB
        IF(ELLIP.LT.1.)CNOCNS=AOB
        CNOCNN=1.0
        IF(ELLIP.LT.1.)CNOCNN=1.5*SQRT(AOB)*(-1./AOB**2/(1.-1./AOB**2)
     1     **1.5*ALOG(AOB*(1.+SQRT(1.-1./AOB**2)))+1./(1.-1./AOB**2))
        IF(ELLIP.GT.1.)CNOCNN=1.5*SQRT(1./AOB)*(AOB**2/(AOB**2-1.)**1.5
     1                 *ATAN(SQRT(AOB**2-1.))-1./(AOB**2-1.))
        CN(I) = (A3*FA*CNOCNS+SP*ETA*CDC(I)*SA*ABS(SA)*CNOCNN)/SREF
        CM(I) = A1*FA*CNOCNS+A2*ETA*CDC(I)*SA*ABS(SA)*CNOCNN
        CA(I) = CD0*CS**2
        CL(I) = CN(I)*CS-CA(I)*SA
        CD(I) = CA(I)*CS+CN(I)*SA
        CNPOT(I)=A3*FA*CNOCNS/SREF
        CNVIS(I)=SP*ETA*CDC(I)*SA*ABS(SA)*CNOCNN/SREF
        CMPOT(I)=A1*FA*CNOCNS
        CMVIS(I)=A2*ETA*CDC(I)*SA*ABS(SA)*CNOCNN
 1010 CONTINUE
      RETURN
      END
