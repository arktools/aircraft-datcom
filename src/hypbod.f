      SUBROUTINE HYPBOD(NMN)
C
C*** COMPUTES HYPERSONIC BODY AERO
C
      REAL MACH,K,LX(20),KTHETA(20)
      REAL INTGCN(20),INTGCM(20),INDEP(20),NXX
      REAL INTGCA(20),KAF(20)
      DIMENSION CAF(20)
      DIMENSION CD(20),CL(20),CDALP(20),ALPHA(20)
      DIMENSION VAR(4),LGH(4)
      DIMENSION FIG3(4)
      DIMENSION THETA(20),DEP(20),DEP1(20),CN(20),CM(20)
      DIMENSION DEP2(20)
      DIMENSION T422(18),D422(81)
      DIMENSION T425(18),D425(72)
      DIMENSION T468(8),D468(8)
      DIMENSION X27M(4),X27I(4),C27(6)
      DIMENSION FIG(4),ROUT(2),FIG1(4),FIG2(4)
      COMMON /OVERLY/ NLOG,NMACH,I,NALPHA
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD
      COMMON /FLGTCD/ FLC(73)
      COMMON /OPTION/ SR,CBAR,ROUGHC,BLREF
      COMMON /BODYI/  NXX,X(20),S(20),P(20),R(20),ZU(20),ZL(20),BNOSE,
     1                BTAIL,RLN,RLA,DS
      COMMON /SYNTSS/ XCG,XW,ZW,ALIW,ZCG,XH,ZH,ALIH,XV,VERTUP,HINAX,
     1                XVF,SCALE,ZV,ZVF,YV,YF,PHIV,PHIF
      COMMON /WINGI/  WINGIN(100)
      COMMON /HTI/    HTIN(154)
      COMMON /WINGD/  A(195)
      COMMON /HTDATA/ AHT(195)
      COMMON /SUPBOD/ SBD(229)
      COMMON /IBODY/  PB, BODY(400)
      COMMON /FLOLOG/ FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1                HEAD,PRPOWR,JEQPOW,LOASRT,TVTPAN,
     2                SUPERS,SUBSON,TRANSN,HYPERS
      LOGICAL FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1        HEAD,PRPOWR,JEQPOW,LOASRT,TVTPAN,
     2        SUPERS,SUBSON,TRANSN,HYPERS
      EQUIVALENCE (ALPHA(1),FLC(23))
      EQUIVALENCE (CDP,SBD(227)),(RLBP,SBD(1))
      EQUIVALENCE (CNANF,SBD(125)),(XCPLN,SBD(126)),(THETAN,SBD(127)),
     1(CNAN,SBD(128)),(CMAN,SBD(129)),(THETAA,SBD(130)),(CNAAF,SBD(131))
     2,(CMAAF,SBD(132)),(CMAA,SBD(134)),(CNAA,SBD(133)),(THETAT,SBD(135)
     3),(CNATF,SBD(136)),(CMATF,SBD(137)),(CNAT,SBD(138)),(CMAT,SBD(139)
     4)
      EQUIVALENCE (K,SBD(140)),(THETA(1),SBD(141)),(LX(1),SBD(161)),
     1            (INTGCN(1),SBD(181)),(INTGCM(1),SBD(201))
      EQUIVALENCE (RLB,SBD(2)),(RLBT,SBD(3)),(DN,SBD(4)),(D1,SBD(5)),
     1       (D2,SBD(6)),(FN,SBD(10)),(CNA,SBD(18)),(CL(1),BODY(21)),
     2    (CMA,SBD(110)),(SS,SBD(111)),(RLCOFF,SBD(113)),(CDF,SBD(115)),
     3  (CD0,SBD(124)),(CD(1),BODY(1)),(CN(1),BODY(61)),(CM(1),BODY(41))
      EQUIVALENCE (RNN,SBD(221)),(CFINC,SBD(222)),(CFCOCF,SBD(223)),
     1            (CDPN,SBD(224)),(CDPA,SBD(225)),(CDPT,SBD(226)),
     2            (CAF(1),BODY(81))
      LOGICAL TAIL,NOSTAL,NOSCYL,NOSCT
      DATA FIG  /4HRADI,4HUS V,4HS X-,4HCOOR/,ROUT/4HHYPB,4HOD  /
      DATA FIG1 /4H4.2.,4H1.1-,4H24  ,4HCNA /
      DATA FIG2 /4H4.2.,4H2.1-,4H27  ,4HXCP /
      DATA FIG3 /4H4.2.,4H3.1-,4H68 (,4HCFC)/
      DATA X27M /0.0,1.0,2.0,3.0/
C
C              FIGURE 4.2.1.1-24
C
      DATA T425/
     11.,1.5,2.,3.,4.,5.,6.,7.,0.0,
     20.,.1,.2,.3,.4,.5,.6,.7,.8/
      DATA D425/
     11.6,1.766,1.850,1.930,1.960,1.976,1.987,1.990,
     21.6,1.764,1.850,1.921,1.950,1.968,1.975,1.979,
     31.592,1.758,1.840,1.904,1.930,1.942,1.950,1.950,
     41.581,1.740,1.810,1.864,1.889,1.900,1.901,1.900,
     51.560,1.704,1.761,1.809,1.822,1.830,1.835,1.838,
     61.530,1.650,1.693,1.729,1.739,1.743,1.750,1.750,
     71.488,1.565,1.604,1.629,1.634,1.640,1.640,1.642,
     81.419,1.463,1.489,1.506,1.510,1.513,1.516,1.515,
     91.320,1.332,1.342,1.354,1.360,1.365,1.369,1.370/
C
C              FIGURE 4.2.2.1-27
C
      DATA T422/
     11.,2.,3.,4.,5.,6.,7.,8.,9.,
     20.0,.1,.2,.3,.4,.5,.6,.7,.8/
      DATA D422/
     1.496,.470,.468,.468,.467,.467,.467,.467,.467,
     2.486,.457,.453,.451,.450,.448,.447,.446,.445,
     3.475,.441,.436,.433,.430,.428,.426,.425,.424,
     4.462,.426,.419,.414,.410,.407,.405,.403,.401,
     5.450,.408,.400,.394,.389,.385,.382,.380,.377,
     6.438,.390,.379,.371,.365,.360,.356,.353,.349,
     7.423,.367,.353,.344,.336,.329,.324,.320,.317,
     8.405,.337,.317,.306,.297,.291,.286,.282,.278,
     9.381,.296,.274,.260,.249,.242,.236,.231,.227/
C
C              FIGURE 4.2.3.1-68
C
      DATA T468
     1/0.,.5,1.,2.,3.,4.,5.,7./
      DATA D468
     1/1.,.975,.927,.775,.616,.500,.409,.275/
C
      NX=NXX+.5
      MACH=FLC(NMN+2)
      RLBP=RLN+RLA
      RLB=X(NX)
      RLBT=RLB-RLBP
      RATIO=ABS(RLBT/RLB)
      IF(RATIO.LT.0.01)GO TO 1000
      TAIL=.TRUE.
      GO TO 1010
 1000 TAIL=.FALSE.
      RLBT=0.0
 1010 VAR(1)=RLN
      LGH(1)=NX
      CALL INTERX(1,X,VAR,LGH,R,DIA,NX,NX,0,0,0,0,0,0,0,0,FIG,4,ROUT)
      DN=2.*DIA
      IF(RLA.EQ.0.0) GO TO 1020
      VAR(1)=RLBP
      CALL INTERX(1,X,VAR,LGH,R,DIA,NX,NX,0,0,0,0,0,0,0,0,FIG,4,ROUT)
      D1=2.*DIA
      GO TO 1030
 1020 D1=DN
 1030 CONTINUE
      IF(.NOT.TAIL) GO TO 1040
      VAR(1)=RLB
      CALL INTERX(1,X,VAR,LGH,R,DIA,NX,NX,0,0,0,0,0,0,0,0,FIG,4,ROUT)
      D2=2.*DIA
      GO TO 1050
 1040 D2=D1
 1050 CONTINUE
 1110 CONTINUE
C
C  ***HYPERSONIC BODY LIFT AND PITCHING MOMENT SLOPES***
C
      FN=RLN/DN
      ARG=.5*(DN-DS)/(DN*FN-.5*DS)
      THETAN=ATAN(ARG)
      BLN=RLN
      BLA=RLA
      BDN=DN
      BD1=D1
      BD2=D2
C
C  ----HERE FOR OGIVE NOSE----
C
      IF(BNOSE.EQ.1.)GO TO 1120
C
C              FIGURE 4.2.1.1-24 (CNA)
C
      VAR(1)=BLN/BDN
      VAR(2)=DS/BDN
      LGH(1)= 8
      LGH(2)= 9
      CALL INTERX(2,T425,VAR,LGH,D425,CNANF,9,72,0,0,0,0,0,0,0,0,FIG1,
     1            4,ROUT)
C
C              FIGURE 4.2.2.1-27
C
      LGH(1)= 9
      LGH(2)= 9
      CALL INTERX(2,T422,VAR,LGH,D422,XCPLN,9,81,0,0,0,0,0,0,0,0,FIG2,
     1            4,ROUT)
      GO TO 1130
 1120 CONTINUE
C
C  ----HERE FOR CONE NOSE----
C              FIGURE 4.2.1.1-24(CNA)N  EQUATION FOR CNANF
C
      ARG1=2.*COS(THETAN)**2
      ARG2=1.-.5*(DS/BDN)**2*COS(THETAN)**2
      CNANF=ARG1*ARG2
C
C              FIGURE 4.2.2.1-26(XCP/LN) EQUATION FOR XCPLN
C
      ARG1=2./(3.*COS(THETAN)**2)
      ARG=DS*COS(THETAN)/BDN
      SINT=SIN(THETAN)
      ARG2=1.-1.5*(1.-SINT)*ARG
      ARG3=.25*(2.-3.*SINT)*ARG**3
      ARG4=1.-.5*ARG**2
      ARG5=1.-ARG/(1.+SINT)
      XCPLN=ARG1*((ARG2+ARG3)/(ARG4*ARG5))
 1130 ANN=PI*BDN**2/4.
      CNAN=CNANF*ANN/(RAD*SR)
      CMAN=CNAN*(XCG/BLN-XCPLN)*BLN/CBAR
      AAA=PI*BD1**2/4.
      IF(BLA.EQ.0.0)GO TO 1140
C
C  ----HERE FOR CENTER SECTION----
C
      RATIOA=DN/BD1
      THETAA=ATAN(.5*(D1-DN)/RLA)
      IF(RATIOA.GE.1.0)GO TO 1140
C
C              FIGURE 4.2.1.1-26(CNA)A EQUATION FOR CNAAF
C
      CNAAF=2.*COS(THETAA)**2*(1.-RATIOA**2)
C
C              FIGURE 4.2.2.1-25A(CMA)A EQUATION FOR CMAAF
C
      ARG1=3.*TAN(THETAA)
      ARG2=2.*(1.-RATIOA**3)
      ARG3=3.*RATIOA*COS(THETAA)**2*(1.-RATIOA**2)
      CMAAF=-(ARG2-ARG3)/ARG1
      GO TO 1150
 1140 CNAAF=0.0
      CMAAF=0.0
 1150 CNAA=CNAAF*AAA/(RAD*SR)
      ARG1=CNAA*(XCG-BLN)/CBAR
      CMAA=CMAAF*AAA*D1/(CBAR*SR) + ARG1
      ATT=PI*BD1**2/4.
      IF(.NOT.TAIL)GO TO 1160
C
C  ----HERE FOR TAIL SECTION----
C
      RATIOT=BD2/BD1
      THETAT=ATAN(.5*(D2-D1)/RLBT)
C
C***  SKIP THIS CALCUALTION IF BOATTAIL
C
      IF(RATIOT.LE.1.0)GO TO 1160
      SBD(16)=THETAT
      SBD(102)=THETAT
C
C              FIGURE 4.2.1.1-26(CNA)T EQUATION FOR CNATF
C
      CNATF=2.*COS(THETAT)**2*(1.-RATIOT**2)
C
C              FIGURE 4.2.2.1-25A(CMA)T EQUATION FOR CMATF
C
      ARG1=3.*TAN(THETAT)
      ARG2=2.*(1.-RATIOT**3)
      ARG3=3.*RATIOT*COS(THETAT)**2*(1.-RATIOT**2)
      CMATF=-(ARG2-ARG3)/ARG1
      GO TO 1170
 1160 CNATF=0.0
      CMATF=0.0
      SBD(14)=THETAT
 1170 CNAT=CNATF*ATT/(RAD*SR)
      ARG1=CNAT*(XCG-BLN-BLA)/CBAR
      CMAT=CMATF*ATT*BD2/(CBAR*RAD*SR)+ARG1
      CMA=CMAN+CMAA+CMAT
      CNA=CNAN+CNAA+CNAT
C
C  ***HYPERSONIC BODY NORMAL FORCE AND PITCHING MOMENT AT ANG OF ATTACK*
C
      NN=NX-1
      DO 1180 N=1,NN
         CALL TBFUNX(X(N),DUM,ARG,NX,X,R,C27,1,1,1,-1,-1,1HX,1,ROUT)
         THETA(N)=RAD*ATAN(ARG)
 1180 LX(N)=XCG-X(N)
      THETA(NX)=THETA(NN)
      LX(NX)=XCG-X(NX)
      K=1.833*(1.-0.4545/MACH**2)
      FACTOR=K*RLB/SR
      DO 1220 J=1,NALPHA
         ARG=ABS(ALPHA(J)/RAD)
         SA=SIN(ARG)
         CA=COS(ARG)
         TA=TAN(ARG)
         DO 1210 N=1,NX
            ARG=THETA(N)/RAD
            TN=TAN(ARG)
            CNN=COS(ARG)
            SN=SIN(ARG)
C
C**   COMPUTE PHI
C
            IF(ABS(ALPHA(J)/RAD) .GT. ABS(ARG)) GO TO 1185
            IF(ARG .GT. 0.0) PHE=0.0
            IF(ARG .LE. 0.0) PHE=PI
            GO TO 1190
 1185       CONTINUE
            PHE=ACOS(TN/TA)
 1190       CONTINUE
C
C***  INTEGRATE ALONG BODY FOR NORMAL AND AXIAL FORCE
C     METHOD FROM NASA TN D-176
C
            SP=SIN(PHE)
            CP=COS(PHE)
            ARG1=(2./3.)*(CNN*SA)**2*SP*(CP**2+2.)
            ARG2=4.*SN*CNN*CA*SA*(PI/2.-.5*SP*CP-PHE/2.)
            ARG3=2.*(SN*CA)**2*SP
            KTHETA(N)=ARG1+ARG2+ARG3
            ARG6=2.*(CA*SN)**2*TN*(PI-PHE)
            ARG7=4.*CA*SA*SP*SN**2
            ARG8=CNN*SN*SA**2*(PI-PHE-SP*CP)
            KAF(N)=ARG6+ARG7+ARG8
 1200       DEP(N)=KTHETA(N)*R(N)
            INDEP(N)=X(N)/RLB
            DEP1(N)=DEP(N)*LX(N)
            DEP2(N)=KAF(N)*R(N)
 1210    CONTINUE
         CALL TRAPZ(DEP,NX,INDEP,INTGCN(J),1)
         CALL TRAPZ(DEP1,NX,INDEP,INTGCM(J),1)
         CALL TRAPZ(DEP2,NX,INDEP,INTGCA(J),1)
         SIGN=1.0
         IF(ALPHA(J).LT.0.0)SIGN=-1.0
         CN(J)=SIGN*FACTOR*INTGCN(J)
         CM(J)=SIGN*FACTOR*INTGCM(J)/CBAR
         CAF(J)=FACTOR*INTGCA(J)
 1220 CONTINUE
C
C***  COMPUTE LIFT AND DRAG FROM CN AND CA
C
      DO 1230 J=1,NALPHA
         ARG=ALPHA(J)/RAD
         CL(J)=CN(J)*COS(ARG)-CAF(J)*SIN(ARG)
         CD(J)=CAF(J)*COS(ARG)+CN(J)*SIN(ARG)
 1230 CONTINUE
      RETURN
      END
