      SUBROUTINE BODYRT
C
C***  COMPUTES AXISYMMETRIC BODY CL,CD,CM
C
      COMMON /FLGTCD/ FLC(160)
      COMMON /OVERLY/ NLOG,NMACH,M,NALPHA,IG
      COMMON /FLOLOG/ FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1                HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2                TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3                HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4                VFPL,VFSC,CTAB
      COMMON /OPTION/ SREF, CBARR, ROUGFC
      COMMON /WINGD/  A(195), B(48)
      COMMON /BDATA/  BD(762)
      COMMON /IBODY/  PBODY, BODY(400)
      COMMON /BODYI/  XNX,X(20),S(20),P(20),R(20),ZU(20),ZL(20),
     1                BNOSE,BTAIL,BLN,BLA,DS
      COMMON /CONSNT/ PI, DEG, UNUSED, RAD
C
      LOGICAL PBODY
      LOGICAL  FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1         HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2         TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3         HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4         VFPL,VFSC,CTAB
C
      DIMENSION ROUTID(2),QSLNSE(4),QDSDX(3),QS0X0(4),QSLNLN(5),Q21120(3
     1          ),Q15127(3),Q1217B(3),Q1217A(3)
      DIMENSION XE(20),RE(20),PE(20),SE(20),DSEDX(20)
     1          ,TST(20),DPEDX(20),DREDX(20)
      DIMENSION CXS(6),CXNOSE(6)
      DIMENSION X1217A(8),Y1217A(8),C1217A(6)
      DIMENSION X21120(10),Y21120(10),C21120(6)
      DIMENSION CNSE(6)
      DIMENSION C(6)
      DIMENSION X1217B(14),Y1217B(14),C1217B(6)
      DIMENSION X27M(4),X27I(4),C27(6)
      DATA ROUTID/4HBODY,4HRT  /,QSLNSE/4HCOMP,4HUTE ,4HSLNO,4HSE  /,
     1 QDSDX/4HFIND,4H DSD,4HX   /,QS0X0/4HFIND,4H S0 ,4HAT X,4H0   /,
     2 QSLNLN/4HFIND,4H SLN,4HOSE ,4HAT L,4HNOSE/,Q21120/4H4.2.,4H1.1-,
     3 4H20  /,Q15127/4H4.1.,4H5.1-,4H27  /,Q1217A/4H4.2.,4H1.2-,4H35A /
     4,Q1217B/4H4.2.,4H1.2-,4H35B /
      DATA X27I/1.57780,1.67221,1.98509,2.28874/
      DATA X27M/0.0,1.0,2.0,3.0/
      DATA I27/0/
C
C     ----FIGURE 4.2.1.2-35B MC LT 1.
C
      DATA I1217B /0/
      DATA X1217B /0.,.2,.3,.36,.4,.5,.6,.7,.77,.8,.86,.9,.98,1./
      DATA Y1217B /1.2,1.2,1.21,1.23,1.27,1.36,1.5,1.67,1.75,1.77,3*1.8,
     1             1.79/
C
C     ----FIG 4.2.1.2-35A
C
      DATA I1217A /0/
      DATA X1217A/2.,4.,8.,12.,16.,20.,24.,28./
      DATA Y1217A /.56,.6,.66,.71,.74,.76,.775,.79/
C
C     ----4.2.1.1-20
C
      DATA X21120 /4.,5.,6.,8.,10.,12.,14.,16.,18.,20./
      DATA Y21120 /.77,.825,.865,.91,.94,.955,.965,.97,.973,.975/
      DATA I21120 /0/
      NX = XNX + .5
      INSE=0
      IXS=0
      INOSE=0
      IN=0
      BD(1)=X(NX)
      BD(57)=S(NX)
      BD(94)=FLC(2)
      BD(55)=12.*BD(1)/ROUGFC
C
C     ----SET LNOSE IF BODY ALONE
C
      BD(5) = X(NX)
C
C     ----COMPUTE SLNOSE
C
      CALL TBFUNX(BD(5),BD(4),DYDX,NX,X,S,CNSE,INSE,MI,NG,
     1 0,0,QSLNSE,4,ROUTID)
C
C     ----SEARCH FOR SMAX
C
      CALL GETMAX(X,S,NX,BD(2),BD(3),IMAX)
      BD(56)=BD(3)
C
C*****ADJUST BASE AREA IF BODY IS BOAT TAILED ********
C
      IF(BD(57).LE.0.3*BD(56)) BD(57)=0.30*BD(56)
      DO 1000 K=2,NX
         IF(S(K).GE.S(K-1))GO TO 1000
         GO TO 1010
 1000 CONTINUE
C
C     ----HERE FOR BODY NEVER DECREASING
C
      X1=X(NX)
      GO TO 1030
C
C     ----FIG 4.2.1.1-20
C
 1010 DO 1020 K=1,NX
         CALL TBFUNX(X(K),SX,DSDX,NX,X,S,C,IN,MI,NG,
     1               2,1,QDSDX,3,ROUTID)
         BD(K+174)=-DSDX
 1020 CONTINUE
      CALL GETMAX(X,BD(175 ),NX,X1,DSDXM,IMAX)
 1030 BD(7)=0.378*BD(1)+0.527*X1
      IF(BD(7).GT.BD(5)) GO TO 1040
C
C     ----X0.LE.LNOSE     ----FIND S0 AT X0
C
      CALL TBFUNX(BD(7),BD(6),DYDX,NX,X,S,CXS,IXS,MI,NG,
     1            0,0,QS0X0,4,ROUTID)
      TMP1 = BD(6)
      TMP2 = BD(3)
      TMP3 = BD(1)
      TMP5 = BD(7)
      GO TO 1050
C
C     ----X0.GT.LNOSE     ----CALCULATE SLNOSE AT LNOSE
C
 1040 CALL TBFUNX(BD(5),BD(4),DYDX,NX,X,S,CXNOSE,INOSE,MI,NG,
     1            0,0,QSLNLN,5,ROUTID)
      TMP1 = BD(4)
      TMP2 = BD(4)
      TMP3 = BD(5)
      TMP5 = BD(5)
 1050 TMP4 = TMP3/SQRT(TMP2*4./PI)
C
C     ----4.2.1.1-20
C
      CALL TBFUNX(TMP4,BD(9),DYDX,10,X21120,Y21120,C21120,I21120,MI,NG,
     1            2,1,Q21120,3,ROUTID)
      BODY(101)=2. * BD(9)*TMP1 /(RAD*SREF)
C
C     -----EQUAL SPACE P AND X ---------
C
      CALL EQSPC1(X,P,NX,XE,PE,20,DPEDX)
      CALL TRAPZ(PE,20,XE,PIN,1)
      DO 1060 L=1,NX
         IF(X(L) .GE. TMP5) GO TO 1070
 1060 CONTINUE
 1070 CONTINUE
      SAVE1 = X(L)
      SAVE2 = S(L)
      X(L)  = TMP5
      S(L)  = TMP1
      DO 1075 K=2,L
      IF(S(K)-S(K-1).EQ.0.0)GO TO 1075
      IL=K
 1075 CONTINUE
      CALL EQSPC1(X,S,IL,XE,SE,20,DSEDX)
C
C     ----CMA COMPUTATION
C
      CONST=2.*BD(9)/(RAD*SREF*CBARR)
      DO 1080 I=1,20
 1080 TST(I)=DSEDX(I)*XE(I)
      CALL TRAPZ(TST,20,XE,TMP,1)
      BODY(121)=(BD(33)*BODY(101)/CBARR)-CONST*TMP
C
C     ----COMPUTE PITCHING MOMENT
C
      CALL EQSPC1(X(L),R(L),NX-L+1,XE,RE,20,DREDX)
      CALL TRAPZ(RE,20,XE,RIN,1)
      BD(88)=RIN
      DO 1090 I=1,20
 1090 TST(I)=RE(I)*XE(I)
      CALL TRAPZ(TST,20,XE,RXDFI,1)
      BD(90)=FLC(M+42)*BD(1)
C
C     ----FIG 4.1.5.1-27 EQUATION
C
      CALL TBFUNX(B(1),CEPT,DYDX,4,X27M,X27I,C27,I27,MI,NG,
     1            0,0,Q15127,3,ROUTID)
      BD(91)=BD(55)**1.0482*10.0**CEPT
      RUSE=BD(90)
      IF(BD(91).LT.BD(90))RUSE=BD(91)
C
C     ----FIG 4.1.5.1-26
C
      ARG=B(1)
      IF(TRANSN)ARG=.60
      CALL FIG26(RUSE,ARG,BD(92))
      BD(93)=PIN
      BD(86)=SQRT(BD(57)*4./PI)
      BD(85)=SQRT(BD(56)*4./PI)
      BD(75)=BD(1)/BD(85)
      BD(59)=BD(92)*(1.+60./(BD(75)**3)+.0025*BD(75))*BD(93)/BD(56)
      BD(60)=.029*((BD(86)/BD(85))**3/SQRT(BD(59))*BD(56)/SREF)
      BD(59)=BD(59)*BD(56)/SREF
      BD(61)=BD(59)+BD(60)
      BD(10)=BD(61)
      X(L) = SAVE1
      S(L) = SAVE2
      IF(TRANSN)RETURN
C
C     ----LOOP ON ALPHA
C
      NALPHA = BD(94)+.5
      DO 1100 J=1,NALPHA
         AP=BD(J+254)
         SINA=SIN(AP/RAD)
         SINA2=SINA**2
         BD(J+154)=BODY(101)*AP
         AMSNA=B(1)*ABS(SINA)
C
C     ----FIGURE 4.2.1.2-35B
C
         CALL TBFUNX(AMSNA,BD(J+134),DYDX,14,X1217B,Y1217B,C1217B,
     1               I1217B,MI,NG,0,0,Q1217B,3,ROUTID)
C
C     ----FIGURE 4.2.1.2-35A
C
         CALL TBFUNX(BD(75),BD(76),DYDX,8,X1217A,Y1217A,C1217A,I1217A,
     1               MI,NG,2,2,Q1217A,3,ROUTID)
         SGN=1.0
         IF(AP.LT.0.)SGN=-1.0
         BD(J+194)=2.*SINA2*BD(76)*BD(J+134)*BD(88)/SREF*SGN
         BODY(J+20)=BD(J+154)+BD(J+194)
C
C     ----CMJ COMPUTATION
C
         BODY(J+40)=BODY(121)*AP-2.*SINA2*BD(J+134)*BD(76)*(RXDFI-
     1              BD(33)*BD(88))/(CBARR*SREF)*SGN
C
C     ----COMPUTE  CDJ
C
         BD(J+214)=BD(J+154)*SINA+BD(J+194)*SINA
         BODY(J)=BD(61)+BD(J+214)
         BODY(J+60)=BODY(J+20)*COS(AP/RAD)+BODY(J)*SINA
         BODY(J+80)=BODY(J)*COS(AP/RAD)-BODY(J+20)*SINA
 1100 CONTINUE
      RETURN
      END
