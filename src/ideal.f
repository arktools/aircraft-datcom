      SUBROUTINE IDEAL
C
C  CALCULATE THE SECTION IDEAL AERODYNAMIC PARAMETERS
C
      COMMON /IBODY/ PB,NACA(80)
      COMMON /IWING/ PW,X0(60)
      COMMON / IBW / PBW,M
      COMMON / IBH / PBH, THN(60),CAM(60),A0
      COMMON / IBV / PBV,ZTNU(32),ZCNU(32),ST1NU(32),ST2NU(32),ST3NU(32)
     1               ,ST4NU(32),ST5NU(32),X(32),THNU(32),THMU(32)
      COMMON /IBWH/  PBWH,AI,ALO,CLI,ALPMAX,CMCO4
      COMMON /IBWHV/ PBWHV, RHO,TMAX
      COMMON /CONSNT/PI,DEG,UNUSED,RAD
      INTEGER A4HS
      DIMENSION C(6)
      DATA A4HS/4HS   /
      XN=32.
      N=XN
      L=N-1
      DO 1000 J=1,L
         XMU=J
 1000 THMU(J)=XMU*PI/XN
      DO 1010 I=1,N
         XNU=I
         THNU(I)=XNU*PI/XN
 1010 X(I)=.5*(COS(THNU(I))+1.)
      A0=SQRT(2.*RHO)
C
C  OBTAIN THICKNESS AT CALCULATED X(I) FOR WEBER ANALYSIS
C
      IF(NACA(8).EQ.A4HS)GO TO 1020
      CALL ASMINT(M,X0,THN,N,X,ZTNU)
      GO TO 1040
 1020 CONTINUE
C
C  HERE FOR SUPERSONIC AIRFOILS
C
      IN=0
      MI=0
      NG=0
      DO 1030 I=1,N
         CALL TBFUNX(X(I),ZTNU(I),DYDX,60,X0,THN,C,IN,MI,NG,0,0,
     1               4HX..Y,1,4HX..Y)
 1030 CONTINUE
 1040 CONTINUE
C
C  DEFINE THE WEBER TERMS FOR THIS AIRFOIL
C
C    ST1NU DEFINES THE SOURCE DISTRIBUTION IN PARALLEL FLOW
C    ST2NU DEFINES THE SLOPE OF THE THICKNESS DISTRIBUTION
C
      DO 1070 I=1,N
         ST1NU(I)=0.0
         ST2NU(I)=0.0
         DO 1070 J=1,L
            IF(I.EQ.J)GO TO 1050
            S1=((-1.)**(J-I)-1.)/XN*2.*SIN(THMU(J))/
     1         (COS(THMU(J))-COS(THNU(I)))**2
            S2=-2.*(-1.)**(J-I)*SIN(THMU(J))/(SIN(THNU(I))*
     1          (COS(THMU(J))-COS(THNU(I))))
            GO TO 1060
 1050       S1=XN/SIN(THNU(I))
            S2=COS(THNU(I))/(SIN(THNU(I)))**2
 1060       YS1=S1*ZTNU(J)
            YS2=S2*ZTNU(J)
            ST2NU(I)=ST2NU(I)+YS2
 1070 ST1NU(I)=ST1NU(I)+YS1
      ST1NU(N)=ST1NU(N)+XN*A0
C
C    ST3NU DEFINES THE VORTEX DISTRIBUTION IN NORMAL FLOW
C
      DO 1100 I=1,N
         ST3NU(I)=0.0
         DO 1100 J=1,L
            IF(I.EQ.J) GO TO 1080
            S3=((-1.)**(J-I)-1.)/XN*2.*SIN(THMU(J))/
     1         (COS(THMU(J))-COS(THNU(I)))**2+2./XN*
     2         (1.-(-1.)**(J-I))/(SIN(THMU(J))*
     3         (COS(THMU(J))-COS(THNU(I))))
            GO TO 1090
 1080       S3=XN/SIN(THNU(I))
 1090       YS3=S3*ZTNU(J)
 1100 ST3NU(I)=ST3NU(I)+YS3
      DO 1130 I=1,N
         IF(I.EQ.N)GO TO 1110
         S3=((-1.)**I-1.)/(XN*(1.+COS(THNU(I))))
         GO TO 1120
 1110    S3=0.0
 1120    Y=S3*SQRT(RHO/2.)
         IF(I.NE.N) GO TO 1130
         Y=XN/2.*A0
 1130 ST3NU(I)=ST3NU(I)+Y
C
C  OBTAIN MEAN LINE DISTRIBUTION AT CALCULATED X(I) FOR WEBER ANALYSIS
C
      CALL ASMINT(M,X0,CAM,N,X,ZCNU)
C
C    ST4NU DEFINES THE VORTEX DISTRIBUTION DUE TO CAMBER
C    ST5NU DEFINES THE SLOPE OF THE MEAN LINE
C
      DO 1160 I=1,N
         ST4NU(I)=0.0
         ST5NU(I)=0.0
         DO 1160 J=1,L
            IF(I.EQ.J)GO TO 1140
            S4=2.*((-1.)**(J-I)-1.)/(XN*SIN(THNU(I)))*(1.-COS(THMU(J))*
     1         COS(THNU(I)))/(COS(THNU(I))-COS(THMU(J)))**2-2.*((-1.)
     2         **J-1.)/(XN*SIN(THNU(I))*(1.-COS(THMU(J))))
            S5=-2.*(-1.)**(J-I)/(COS(THMU(J))-COS(THNU(I)))
            GO TO 1150
 1140       S4=XN/SIN(THNU(I))-2.*((-1.)**J-1.)/(XN*SIN(THNU(I))*
     1         (1.-COS(THNU(I))))
            S5=-COS(THNU(I))/(SIN(THNU(I)))**2
 1150       YS4=S4*ZCNU(J)
            YS5=S5*ZCNU(J)
            ST4NU(I)=ST4NU(I)+YS4
 1160 ST5NU(I)=ST5NU(I)+YS5
C
C  GET PLANFORM IDEAL PARAMETERS
C
      IF(CLI .NE. UNUSED) GO TO 1190
      JN=N-4
      ZC95=ZCNU(4)
      ZC05=ZCNU(JN)
      DAI05= .3739*ZC05+.04745*ST5NU(N-1)
      DAI95=-.3739*ZC95+.04745*ST5NU(1)
      DA95=-.7834*ZC95+.09518*ST5NU(1)
      T1=.5*(-ZC95*COS(THNU(4))/(SIN(THNU(4))/2.)**2-ZC05*
     1  COS(THNU(JN))/(SIN(THNU(JN))/2.)**2)
      T2=.5*ZC95/(1.-COS(THNU(4)))
      NM5=N-5
      SUMAL=0.0
      SUMAI=0.0
      DO 1180 I=5,L
         IF(I .GT. NM5) GO TO 1170
         T3=-ZCNU(I)*COS(THNU(I))/(SIN(THNU(I))/2.)**2
         SUMAI=SUMAI+T3
 1170    T4=ZCNU(I)/(1.-COS(THNU(I)))
 1180 SUMAL=SUMAL+T4
C
C  IDEAL ANGLE OF ATTACK
C
      DAI=.5/XN*(T1+SUMAI)
      AI=RAD*(DAI05+DAI+DAI95)
C
C  ANGLE OF ATTACK FOR ZERO LIFT
C
      DALO=-2./XN*(T2+SUMAL)
      ALO=RAD*(DA95+DALO)
C
C  IDEAL LIFT COEFFICIENT
C
      CLI=2.*PI/RAD*(AI-ALO)
 1190 CONTINUE
C
C  PITCHING MOMENT ABOUT THE QUARTER CHORD
C
      SUMCM=0.0
      DO 1200 I=1,L
         T5=ZCNU(I)*COS(THNU(I))
 1200 SUMCM=SUMCM+T5
      XMUO=SUMCM*(-PI/XN)
      CMCO4=2.*XMUO+PI/2.*ALO/RAD
      RETURN
      END
