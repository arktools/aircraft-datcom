      SUBROUTINE INTKBW(MACH,OLE,CR,D,DX,KBW,XAC)
C
C  SUBROUTINE TO INTEGRATE THE PRESSURE DISTRIBUTION OVER A
C  BODY, BEHIND THE SHOCK, AND OBTAIN THE FIN/BODY INTERFERENCE
C  FACTORS.  METHOD FROM NACA TR 1307.
C
C *** INPUTS ***
C
C   MACH -- FREE-STREAM MACH NUMBER
C    OLE -- FIN LEADING EDGE SWEEP, DEGREES
C     CR -- FIN EXPOSED ROOT CHORD
C      D -- BODY DIAMETER AT FIN
C     DX -- LENGTH OF AFTER BODY BEHIND FIN
C
C *** OUTPUTS ***
C
C    KBW -- INTERFERENCE FACTOR K-B(W)*CONST.
C           CONST IS A CONSTANT TO RELATE TO PANEL LIFT, TAPER RATIO,
C           ASPECT RATIO AND SPAN/DIAMETER RATIO
C    XAC -- AERODYNAMIC CENTER (XAC*CONST)
C
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD
      REAL MACH,M,N,LL,KBW
      DIMENSION DATA(50),SAVE(50)
      DIMENSION DATM(50),SAVM(50)
C
      NN=50
      IF(MACH .LE. 1.)GO TO 1050
      XX   = NN-1
C
      M=ABS(1./TAN(OLE/RAD))
      DN   = D/XX
      BETA = SQRT(MACH**2-1.)
      IF(DX .LT. (D*BETA-CR)) DN=(CR+DX)/(XX*BETA)
      IF(BETA*M .LE. 1.0) GO TO 1020
C
      DO 1010 I=1,NN
        N  = DN*FLOAT(I-1)
        LL = BETA*N
        UL = CR+LL
        IF(UL .GT. (CR+DX)) UL = CR+DX
        DE = (UL-LL)/XX
        DO 1000 J = 1,NN
          E = LL+DE*FLOAT(J-1)
          IF(N .EQ. 0.0)   VAL = 1./(BETA*M)
          IF(N .NE. 0.0)   VAL = (E/BETA+BETA*M*N)/(N+M*E)
          IF(VAL .GT. 1.0) VAL = 1.0
          DATA(J) = ACOS(VAL)
          DATM(J) = E*DATA(J)
 1000   CONTINUE
        CALL QUADIN(DATA,NN,DE,SAVE(I))
        CALL QUADIN(DATM,NN,DE,SAVM(I))
 1010 CONTINUE
      CALL QUADIN(SAVE,NN,DN,KBW)
      CALL QUADIN(SAVM,NN,DN,XAC)
      XAC = XAC/(KBW*CR)
      KBW = 8.*BETA*M/(PI*(D*CR/2.)*SQRT(BETA**2*M**2-1.))*KBW
      GO TO 1050
C
 1020 CONTINUE
      DO 1040 I=1,NN
        N  = DN*FLOAT(I-1)
        LL = BETA*N
        UL = CR+LL
        IF(UL .GT. (CR+DX)) UL = CR+DX
        DE = (UL-LL)/XX
        DO 1030 J = 1,NN
          E = LL+DE*FLOAT(J-1)
          IF(N .EQ. 0.0)   VAL = 1./(BETA*M)
          IF(N .NE. 0.0)   VAL = (E/BETA-N)/(N+M*E)
          IF(VAL .LT. 0.0) VAL = 0.0
          DATA(J) = SQRT(VAL)
          DATM(J) = E*DATA(J)
 1030   CONTINUE
        CALL QUADIN(DATA,NN,DE,SAVE(I))
        CALL QUADIN(DATM,NN,DE,SAVM(I))
 1040 CONTINUE
      CALL QUADIN(SAVE,NN,DN,KBW)
      CALL QUADIN(SAVM,NN,DN,XAC)
      XAC = XAC/(KBW*CR)
      KBW = 16.*(BETA*M)**1.5/(PI*(D*CR/2.)*(BETA*M+1.))*KBW
 1050 CONTINUE
      RETURN
      END
