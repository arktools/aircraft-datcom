      SUBROUTINE TRNYRL
C
C  BEFORE CALLING TRNYRL, SET M=0.6 AND CALL LATFLP,GDELTA, AND LIFTFP.
C
      COMMON /OVERLY/ NLOG,NMACH,IM,NALPHA
      COMMON /FLGTCD/ FLC(93)
      COMMON /OPTION/ SREF,CBARR,RUFF,BLREF
      COMMON /FLAPIN/ F(69)
      COMMON /HTI/    HTIN(131)
      COMMON /IBODY/  PBODY, BODY(400)
      COMMON /IWING/  PWING, WING(400)
      COMMON /IHT/    PHT, HT(380)
      COMMON /WINGD/  A(195)
      COMMON /HTDATA/ AHT(195)
      COMMON /IDWASH/ PDWASH, DWASH(60)
      COMMON /SBETA/  STB(135),TRA(108),TRAH(108)
      COMMON /SUPDW/  DWA(35),TCD(58)
      COMMON /CONSNT/ PI,DR,UNUSED,RADS
      COMMON /POWR/   PW(59),FLA(45),FLP(189),TRN(7)
      COMMON /FLOLOG/ FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1                HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,
     2                SUPERS,SUBSON,TRANSN,HYPERS,
     3                SYMFP,ASYFP,TRIMC,TRIM
      LOGICAL FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1        HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,
     2        SUPERS,SUBSON,TRANSN,HYPERS,
     3        SYMFP,ASYFP,TRIMC,TRIM
      LOGICAL TRANSL
      REAL MACH,KHB,KBH
      DIMENSION ROUTID(2)
      DIMENSION Q21222(3)
      DIMENSION Q212A1(4),Q212A2(4)
      DIMENSION DELCL6(10),DELCL(10)
      DIMENSION CFACT(10),CLALDL(10)
      DIMENSION CLAFS(10),C21222(6)
      DIMENSION DELTAL(10),DELTAR(10)
      DIMENSION BD(4)
      DIMENSION CNYM6(200),CNAFS(200)
      DIMENSION CLRLM6(10)
      DIMENSION CLROLT(10)
      DIMENSION X12A1(11),Y12A1(11),C12A1(6)
      DIMENSION X12A2(11),Y12A2(11),C12A2(6)
      DIMENSION X12222(6),Y21222(6)
      EQUIVALENCE (CNAFS(1),BODY(201)),(CLALDL(1),WING(241)),
     1            (CLAFS(1),HT(211))
      EQUIVALENCE (CLROLT(1),CLAFS(1)),(DELCL,WING(201)),
     1            (ENCEPE,TRN(1)),(YH,TRN(2))
      EQUIVALENCE (ETAQRS,TRN(3)),(CLDELC,TRN(4)),
     1            (CLDALC,TRN(5))
      EQUIVALENCE (DELCL6(1),WING(201))
      EQUIVALENCE (CFACT(1),FLP(71))
      EQUIVALENCE (CLRLM6(1),HT(211))
      EQUIVALENCE (CNYM6(1),BODY(201))
      EQUIVALENCE (ASTRW,A(7)),(DEPSDA,DWASH(41)),(CLAH,HT(101))
      EQUIVALENCE (BD(1),TCD(43))
      EQUIVALENCE (SSPN,HTIN(4)),(SSPNE,HTIN(3))
      EQUIVALENCE (DELTAL(1),F(19)),(DELTAR(1),F(29))
      EQUIVALENCE (CNAH,CLAH)
      EQUIVALENCE (TRANSL,FLP(59))
      EQUIVALENCE (KBH,TRN(6)),(KHB,TRN(7))
      DATA IN1222/0/
C
C*********          FIGURE 4.3.1.2-12-A1
C
      DATA X12A1 /0.0,0.1,0.2,0.3,0.4,0.5,0.6,0.7,0.8,0.9,1.0/
      DATA Y12A1 /1.0,0.97,0.95,4*0.94,0.95,0.96,0.98,0.99/
      DATA I12A1 /0/
      DATA Q212A1/4H4.3.,4H1.2-,4H12-A,4H1   /
C
C*********          FIGURE 4.3.1.2-12-A2
C
      DATA X12A2 /0.0,0.1,0.2,0.3,0.4,0.5,0.6,0.7,0.8,0.9,1.0/
      DATA Y12A2 /0.0,0.11,0.21,0.31,0.41,0.51,0.60,0.70,0.80,0.90,1.0/
      DATA I12A2 /0/
      DATA Q212A2/4H4.3.,4H1.2-,4H12-A,4H2   /
      DATA ROUTID/4HTRNY,4HRL  /
C
C     FIGURE 6.2.1.2-22
C
      DATA X12222 /.165,.2,.25,.3,.35,.40/
      DATA Y21222 /.830,.818,.779,.738,.680,.615/
      DATA Q21222 /4H6.2.,4H1.2-,4H22  /
C
      TRANSL=.TRUE.
      IF(F(17).EQ.1 .OR. F(17).EQ.5 .OR. F(17).EQ.6) TRANSL=.FALSE.
      MACH=FLC(IM+2)
      NDELTA=F(16)+.5
      NCROL=NDELTA*NALPHA
      STYPE=F(18)
      CLAW=WING(101)
      CLAM6=TRA(70)
      SAVE=CLAW/CLAM6
      IF(ASYFP)GO TO 1030
      IF(HTPL)CLAW=CNAH
      IF(HTPL)SAVE=CLAW/TRAH(70)
C
C     TRANSONIC LIFT COEFFICIENT INCREMENT
C
      DO 1000 I=1,NDELTA
         DELCL(I)=DELCL6(I)*SAVE
 1000 CONTINUE
C
C     WING LIFT CURVE SLOPE WITH TRANSLATING LEADING EDGE AND
C     TRAILING EDGE FLAPS AND SLATS
C
      IF(.NOT.TRANSL)GO TO 1020
      DO 1010 I=1,NDELTA
         CLALDL(I)=CLAW*(1.+CFACT(I))+CLAW
 1010 CONTINUE
      RETURN
 1020 CONTINUE
      DO 1025 I=1,NDELTA
         CLALDL(I)=UNUSED
 1025 CONTINUE
      RETURN
 1030 CONTINUE
      IF(STYPE.EQ.5.)GO TO 1060
C
C     TRANSONIC ROLLING MOMENT DUE TO DEFLECTION OF PLAIN FLAPS AND
C    SPOILERS ON WING
C
      DO 1040 I=1,NDELTA
         CLAFS(I)=CLRLM6(I)*SAVE
 1040 CONTINUE
      IF(STYPE.NE.4.)RETURN
C
C     TRANSONIC YAWING MOMENT DUE TO DEFLECTION OF
C
C     PLAIN TRAILING EDGE FLAPS
      DO 1050 J=1,NCROL
         IF(CNYM6(J) .EQ. UNUSED) GO TO 1050
         CNAFS(J)=CNYM6(J)*SAVE
 1050 CONTINUE
      RETURN
 1060 CONTINUE
C
C     TRANSONIC ROLLING MOMENT DUE TO DEFLECTION OF
C     ALL MOVING HORIZONTAL TAIL
C
      RAD=SSPN-SSPNE
      ENCEPE=(.352*BD(1)+.503*BD(2)+.344*BD(3)+.041*BD(4))/(.383*BD(1)+
     1       .707*BD(2)+.924*BD(3)+.5*BD(4))
      IF(MACH.GE.1.)GO TO 1080
      YH=ENCEPE*SSPNE+RAD
      DHBH=RAD/SSPN
      CALL TBFUNX(DHBH,ETAQRS,DYDX,4,X12222,Y21222 ,C21222,IN1222,MI1222
     1           ,NG,1,2,Q21222,3,ROUTID)
      CLDELC=(.5*(1.-(PI*ASTRW/RADS)*DEPSDA)*ETAQRS*YH*CLAH)/BLREF
      DO 1070 I=1,NDELTA
         CLROLT(I)=CLDELC*(DELTAL(I)-DELTAR(I))
 1070 CONTINUE
      GO TO 1120
 1080 YH=.4*SSPNE+RAD
      RATIO=(SSPN-SSPNE)/(2.*SSPN)
 1090 CALL TBFUNX(RATIO,KHB  ,DYDX,11,X12A1,Y12A1,C12A1,I12A1,MI,NG,
     1            0,0,Q212A1,4,ROUTID)
 1100 CALL TBFUNX(RATIO,KBH  ,DYDX,11,X12A2,Y12A2,C12A2,I12A2,MI,NG,
     1            0,0,Q212A2,4,ROUTID)
      CLDALC=.35*(KHB+KBH)*CNAH*YH/BLREF
      DO 1110 K=1,NDELTA
         CLROLT(K)=CLDALC*(DELTAL(K)-DELTAR(K))
 1110 CONTINUE
 1120 CONTINUE
      RETURN
      END
