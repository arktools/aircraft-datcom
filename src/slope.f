      SUBROUTINE SLOPE(MACH,CLALPA,RENN,XACM)
C
C***  CALCULATES AIRFOIL SECTION CLA,CM0 AND XAC
C
      COMMON /IBH/    PBH, THN(60),CAM(60),A0,XC,MCC,CLCC
      COMMON /IBV/    PBV,ZTNU(32),ZCNU(32),ST1NU(32),ST2NU(32),
     1                ST3NU(32),ST4NU(32),ST5NU(32),X(32),THNU(32),
     2                THMU(32)
      COMMON /IBWH/   PBWH,AI,ALO,CLI,ALPMAX,CMCO4
      COMMON /IBWHV/  PBWHV,RHO
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD
      COMMON /IPOWER/ PP,VXOVU(32),VXOVL(32),CPUM(32),CPLM(32),
     1                BO(32),CPI(32)
      DIMENSION CL(2),ALPHA(2),CM(2),XI(32),STNU(32)
C
C  WEBER PRESSURE DISTRIBUTION AND CL-ALPHA
C  METHOD INCLUDES COMPRESSIBILITY
C
      REAL LX,MACH,MCC
      XN=32.
      N=XN
      NM1=N-1
      CLALPA=UNUSED
      IF(MACH.GE.1.) GO TO 1070
      ALPHA(1)=ALO
      ALPHA(2)=ALO+1.
      BETA=SQRT(1.-MACH**2)
      A0=SQRT(2.*RHO)
      TMACH=0.
      IF(MACH.NE.0.) TMACH=1./(.7*MACH**2)
 1000 DO 1050 I=1,2
         COSA=COS(ALPHA(I)*DEG)
         SINA=SIN(ALPHA(I)*DEG)
         DO 1020 L=1,NM1
            CPI(L)=1.-(1.+ST1NU(L))**2/(1.+ST2NU(L)**2)
            BO(L)=1.-MACH**2*(1.-MACH*CPI(L))
            IF(BO(L) .LT. 0.) GO TO 1080
            BO(L)=SQRT(BO(L))
            VXOVU(L)=(COSA*(1.+ST1NU(L)/BO(L)+ST4NU(L)/BETA)+SINA/BETA*
     1               (1.+ST3NU(L)/BO(L))*SQRT((1.-X(L))/X(L)))/SQRT(1.
     2               +((ST2NU(L)+ST5NU(L))/BO(L))**2)
            VXOVL(L)=(COSA*(1.+ST1NU(L)/BO(L)-ST4NU(L)/BETA)-SINA/BETA*
     1               (1.+ST3NU(L)/BO(L))*SQRT((1.-X(L))/X(L)))/SQRT(1.
     2               +((ST2NU(L)-ST5NU(L))/BO(L))**2)
            IF(MACH .EQ. 0.) GO TO 1010
            CPUVD=1.+.2*MACH**2*(1.-VXOVU(L)**2)
            IF(CPUVD .LT. 0.) GO TO 1080
            CPLVD=1.+.2*MACH**2*(1.-VXOVL(L)**2)
            IF(CPLVD .LT. 0.) GO TO 1080
            CPUM(L)=TMACH*((CPUVD)**(3.5)-1.)
            CPLM(L)=TMACH*((CPLVD)**(3.5)-1.)
            GO TO 1020
 1010       CPUM(L)=1.-(VXOVU(L))**2
            CPLM(L)=1.-(VXOVL(L))**2
 1020    CONTINUE
C
C  PRESSURE COEFFICIENT AT THE LEADING EDGE
C
         IF(A0.NE.0.)VXOVU(N)=SINA*((1.+ST3NU(N))/(.5*A0))
         IF(A0.EQ.0.)VXOVU(N)=0.
         VXOVL(N)=VXOVU(N)
         CPUVDN=1.+.2*MACH**2*(1.-(VXOVU(N)/BETA)**2)
         IF(CPUVDN .LT. 0.) GO TO 1080
         CPUM(N)=TMACH*((CPUVDN)**(3.5)-1.)
         IF(TMACH .EQ. 0.) CPUM(N)=1.-(VXOVU(N))**2
         CPLM(N)=CPUM(N)
C
C   INTEGRATE THE CP CURVE AT THIS ALPHA
C
         SUMCL=0.0
         DO 1030 L=1,NM1
            LX=-(CPUM(L)-CPLM(L))
            SINTH=SIN(THNU(L))/2.
            SUMCL=SUMCL+LX*SINTH
 1030    CONTINUE
         CL(I)=SUMCL*PI/(COSA*XN)
C
C  PITCHING MOMENT COEFFICIENT
C
         SUMCM=0.0
         DO 1040 L=1,NM1
            LX=CPUM(L)-CPLM(L)
           DCM=LX*(X(L)-.25)*SIN(THNU(L))/2.
 1040    SUMCM=SUMCM+DCM
         CM(I)=SUMCM*PI/XN
 1050 CONTINUE
      IF(ABS(CL(1)) .LE. .001) GO TO 1060
      CLALPA=CL(2)-CL(1)
      ALO=ALO-CL(1)/CLALPA
      ALPHA(1)=ALO
      ALPHA(2)=ALO+1.
      GO TO 1000
 1060 CONTINUE
C
C  GET CORRECTION FACTOR FOR VISCOUS AFFECTS
C
      REN=1.E6
      IF (RENN.GE.10..AND.MCC.NE.0..AND.MACH.NE.0.) REN=RENN
      IF(REN .LT. 2.71828E5)WRITE(6,1170)
      IF(REN .LT. 2.71828E5)REN=2.7182E5
      PHITE=((ZTNU(6)+ZTNU(7))/2.-ZTNU(2))/.089272624
      EXPN=-1.+5.*PHITE/2.
      CON=1.-(ALOG(REN /1.E5))**EXPN*(.232+1.785*PHITE-2.950*PHITE**2)
      IF(CON .LT. .6896) CON=.6896
C
C  GET LIFT CURVE SLOPE FROM THESE TWO ANGLES OF ATTACK
C
      CLALPA=(CL(2)-CL(1))*CON*1.05
C
C  GET PITCHING MOMENT SLOPE
C
      CMALPA=CM(2)-CM(1)
      IF(MACH.EQ.0.0) CMCO4=CM(1)
C
C  NOW THE AERODYNAMIC CENTER
C
      XACM=0.25-CMALPA/CLALPA
 1070 CONTINUE
      GO TO 1090
 1080 CONTINUE
C
C  ANALYSIS FAILING -- PRINT INFORMATIVE DIAGNOSTIC
C
      WRITE(6,1160)
 1090 CONTINUE
      IF(MACH .NE. 0.) GO TO 1150
C
C  CALCULATE THE CREST CRITICAL CONDITIONS FOR THIS AIRFOIL
C
      CLM=CLALPA
      J=N-1
      DO 1100 I=1,NM1
         XI(I)=X(J)
         STNU(I)=ST2NU(J)+ST5NU(J)
 1100 J=J-1
      STC=SINA/COSA
      DO 1110 I=1,NM1
         II=I
         IF(STC .GT. STNU(I)) GO TO 1120
 1110 CONTINUE
 1120 XC=(STC-STNU(II))/(STNU(II-1)-STNU(II))*(XI(II-1)-XI(II))+XI(II)
      DO 1130 I=1,NM1
         II=I
         IF(XC .GT. X(I)) GO TO 1140
 1130 CONTINUE
 1140 VOVU=(XC-X(II))/(X(II-1)-X(II))*(VXOVU(II-1)-VXOVU(II))+VXOVU(II)
      CPCC=1.-VOVU**2
      MCC=1.023-.9507*CPCC-.4140*CPCC**2-.1506*CPCC**3-.0212*CPCC**4
      MCC=1./MCC
      BCC=SQRT(1.-MCC**2)
      CLCC=(ABS(CLM)/(BCC-(MCC**2/(1.+BCC))*ABS(CLM)/2.))*CON
 1150 CONTINUE
 1160 FORMAT(56H ********* COMPUTATIONS IN SUBROUTINE SLOPE HAVE FAILED.
     1,38H ANALYSIS FOR THIS AIRFOIL IMPOSSIBLE.)
 1170 FORMAT(56H0**** REYNOLDS NUMBER TOO LOW FOR THE AIRFOIL SECTION MO
     1,61HDULE, SECTION CHARACTERISTICS BASED ON A VALUE OF 2.718E5 ***)
      RETURN
      END
