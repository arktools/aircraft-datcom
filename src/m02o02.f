      SUBROUTINE M02O02
C
C***  EXEC FOR OVERLAY 2, CALCULATE GEOMETRIES AND SYNTHESIS DATA
C
      COMMON /OVERLY/  IJKDUM(8),NOVLY
      COMMON /OPTION/ SREF, CBARR, ROUGFC, BLREF
      COMMON /SYNTSS/ XCG, XW, ZW, ALIW, ZCG, XH, ZH, ALIH, XV,
     1                VERTUP, HINAX, XVF, SCALE, ZV, ZVF, YV, YF,
     2                PHIV, PHIF
      COMMON /WINGI/  WINGIN(101)
      COMMON /VTI/    VTIN(154), TVTIN(8), VFIN(154)
      COMMON /HTI/    HTIN(154)
      COMMON /WINGD/  A(195), B(49)
      COMMON /HTDATA/ AHT(195), BHT(49)
      COMMON /VTDATA/ AVT(195), AVF(195)
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD,KAND
      COMMON /FLOLOG/ FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1                HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2                TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3                HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4                VFPL,VFSC,CTAB
C
      LOGICAL  FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1         HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2         TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3         HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4         VFPL,VFSC,CTAB
      DIMENSION INDX(12)
      DATA INDX/1,2,3,4,5,6,7,119,120,163,167,168/
C
      NOVLY=2
C
C     ----STORE QUANTITIES IN PROPER LOCATION ECT. TO START COMPUTATIONS
C
      CALL SETUP1
      IF(LOASRT)GO TO 1020
C
C      ----COMPUTE PLANFORM GEOMETRIES IF PRESENT
C
      IF(WGPL) CALL WTGEOM(A,WINGIN)
      IF(HTPL) CALL WTGEOM(AHT,HTIN)
      IF(VTPL) CALL WTGEOM(AVT,VTIN)
      DO 1000 I=1,12
        II=INDX(I)
        IF(AVT(II).NE.UNUSED)AVT(II)=AVT(II)/2.
 1000 CONTINUE
      IF(VFPL) CALL WTGEOM(AVF,VFIN)
      DO 1010 I=1,12
        II=INDX(I)
        IF(AVF(II).NE.UNUSED)AVF(II)=AVF(II)/2.
 1010 CONTINUE
C
      AVT(130) = AVT(130)+ ZV
      AVT(133) = AVT(133)+ ZV
      AVT(136) = AVT(136)+ ZV
      AVF(130) = AVF(130)+ZVF
      AVF(133) = AVF(133)+ZVF
      AVF(136) = AVF(136)+ZVF
C
C     ----IF SREF AND CBARR WERE INPUTS USE INSTEAD OF COMPUTED VALUES.
C
      IF(SREF.EQ.UNUSED)SREF=A(4)
      IF(CBARR.EQ.UNUSED)CBARR=A(122)
      IF(BLREF.EQ.UNUSED)BLREF=2.*WINGIN(4)
C
C     ----SET ROUGHNESS FACTOR IF NOT READ IN
C
 1020 IF(ROUGFC.EQ.UNUSED)ROUGFC=1.6E-04
      IF(LOASRT)GO TO 1030
C
C     ----COMPUTE SYNTHESIZING DIMENSIONS IF WING + BODY PRESENT
C
      IF(WGPL)CALL SYNDIM
C
C      ----CLASSIFY PANEL ASPECT RATIO IF PRESENT
C
      A(128)   = WINGIN(92)
      AHT(128) = HTIN(92)
      AVT(128) = VTIN(92)
      AVF(128) = VFIN(92)
      IF(WGPL) CALL ARCLSS(A)
      IF(HTPL) CALL ARCLSS(AHT)
      IF(VTPL) CALL ARCLSS(AVT)
      IF(VFPL) CALL ARCLSS(AVF)
C
C     ----IF ANY COMBINATIONS COMPUTE INTERFERENCE FACTOR GEOMETRY
C
      IF((WGPL.AND.BO).OR.(HTPL.AND.BO)) CALL INFTGM
 1030 CONTINUE
      RETURN
      END
