      SUBROUTINE SUPLAV
C
C***  SUPERSONIC VERTICAL TAIL LATERAL STABILITY
C
      COMMON /IVT/     PVT,    VT(380)
      COMMON /IBW/     PBW,    BWI(380)
      COMMON /IBWV/    PBWV,   BWV(380)
      COMMON /IBWHV/   PBWHV,  BWHV(380)
C
      COMMON /FLGTCD/ FLC(160)
      COMMON /OPTION/ SREF, CBARR, ROUGFC, BLREF
      COMMON /SYNTSS/ XCG, XW, ZW, ALIW, ZCG, XH, ZHH, ALIH, XV,
     1                VERTUP, HINAX, XVF, SC, ZV, ZVF, YV, YF,
     2                PHIV, PHIF
      COMMON /WINGI/  WINGIN(101)
      COMMON /VTI/    VTIN(154), TVTIN(8), VFIN(154)
      COMMON /HTI/    HTIN(154)
      COMMON /SBETA/  SLA(31)
      COMMON /HTDATA/ AHT(195), BHT(49)
      COMMON /VTDATA/ AVT(195), AVF(195)
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD,KAND
      COMMON /OVERLY/ NLOG,NMACH,I,NALPHA,IG,NF,LF,K
      COMMON /FLOLOG/ FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1                HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2                TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3                HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4                VFPL,VFSC,CTAB
C
      LOGICAL  FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1         HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2         TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3         HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4         VFPL,VFSC,CTAB
      LOGICAL VHPLA, VERTUP
C
      DIMENSION ROUTID(2), QAMF(3), XAMF(3), YAMF(3), CC(6)
      DIMENSION ALPHA(20), SYNA(19)
      DIMENSION RLPH(20), SVB(20), SVWB(20), SVHB(20)
C
      EQUIVALENCE (ALPHA(1),FLC(23))
      EQUIVALENCE (DCYHWB,SLA(23))
      EQUIVALENCE (RKVWB,SLA(24)), (RKVB,SLA(25)), (RKPVWB,SLA(26))
      EQUIVALENCE (DCYBV,SLA(27)), (RKVHB,SLA(28)), (ZP,SLA(29))
      EQUIVALENCE (RLP,SLA(30)), (CNAV,SLA(31))
      EQUIVALENCE (SYNA(1),XCG), (SPAN,WINGIN(4))
      EQUIVALENCE (RLPH(1),HTIN(95)), (SVWB(1),VTIN(95))
      EQUIVALENCE (SVB(1),VTIN(115)), (SVHB(1),VTIN(135))
C
      DATA WTYPE  / 4HSTRA /
      DATA ROUTID / 4HSUPL, 4HAV   /
      DATA QAMF   / 4H5.3., 4H1.1-, 4H25A  /
      DATA XAMF   / 0.0, 0.40, 1.0 /
      DATA YAMF   / 0.5, 1.79, 4.0 /
C
      ZH = ZHH-((HTIN(4)-HTIN(3))*AHT(62)+AHT(30))*SIN(ALIH/RAD)
      SVSTAR = AVT(3)
      VHPLA  = .FALSE.
      SCALE  = 2.0*SPAN/BLREF
C
C       ***VERTICAL TAIL ADDED TO WING-BODY***
C
      R1=VTIN(4)-VTIN(3)
      R1OBA=R1/VTIN(4)
      R2OBO2=R1/WINGIN(4)
      ZHOR1=-ZW/R1
 1000 IF(.NOT.VERTUP)ZHOR1=-ZHOR1
C
C         CALL  SUBROUTINE MASRAT FOR FIG.5.3.1.1-25B-P
C
      CALL MASRAT(R2OBO2,R1OBA,ZHOR1,ARG1)
      IF(VHPLA)GO TO 1010
C
C              FIGURE 5.3.1.1-25A KV(B)
C
      IN = 0
      CALL TBFUNX(R1OBA, RKVB, DYDX, 3, XAMF, YAMF, CC,
     1            IN, MI, NG, 0, 1, QAMF, 3, ROUTID)
      RKVWB=ARG1
      RKPVWB= (RKVB*SVB(I)+RKVWB*SVWB(I))/SVSTAR
      DCYBV=-RKPVWB*CNAV
C
C       ***VERTICAL PANEL ADDED TO WING-BODY-HORIZONTAL TAIL***
C
      IF(.NOT. HTPL) GO TO 1020
      R2OBO2=R1/HTIN(4)
      ZHOR1=ZH/R1
      VHPLA=.TRUE.
      GO TO 1000
 1010 RKVHB=ARG1
      DCVWHB=DCYBV-RKVHB*SVHB(I)/SVSTAR*CNAV
 1020 CONTINUE
C
C  *** WING-BODY-TAIL DERIVATIVES ***
C
      BW=2.*SPAN
      DELX=AVT(62)*(VTIN(4)-VTIN(3))
      RLP=SYNA(9)-SYNA(1)+DELX+AVT(30)
      ZP=-SYNA(5)+VTIN(4)-VTIN(3)+AVT(31)+SYNA(14)
      VT(141) = DCYBV
      VT(161) = -DCYBV*RLP/BLREF
      BWV(141) = BWV(141)+VT(141)
      BWV(161) = BWV(161)+VT(161)
      IF(WINGIN(15).NE.WTYPE) GO TO 1050
      DO 1030 J=1,NALPHA
         ALP=ALPHA(J)/RAD
         DZ=(ZP*COS(ALP)-RLP*SIN(ALP))/BW
      VT(J+180)=DCYBV*DZ*SCALE
      BWV(J+180)=BWV(J+180)+VT(J+180)
 1030 CONTINUE
      IF(.NOT. HTPL) GO TO 1050
      VT(141)=DCVWHB
      VT(161)=-DCVWHB*RLP/BLREF
      BWHV(141)=BWHV(141)+DCVWHB
      BWHV(161)=BWHV(161)-DCVWHB*RLP/BLREF
      DO 1040 J=1,NALPHA
         ALP=ALPHA(J)/RAD
         DZ=(ZP*COS(ALP)-RLP*SIN(ALP))/BW
         VT(J+180) = DCVWHB*DZ*SCALE
         BWHV(J+180)=BWHV(J+180)+VT(J+180)
 1040 CONTINUE
 1050 CONTINUE
      RETURN
      END
