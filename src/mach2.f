      SUBROUTINE MACH2(DNU,XM,IER)
C
C     THIS SUBROUTINE TAKES THE PRANDTL-MEYER ANGLE (DNU) AND SOLVES THE
C     TRANSCENDENTIAL EQUATION FOR THE FLOW MACH NUMBER USING NEWTONS
C     METHOD
C
C     DESCRIPTION OF PARAMETERS -
C       DNU - ARGUMENT VALUE OF PRANDTL-MEYER ANGLE IN DEGREES
C       XM  - RESULTING MACH NUMBER
C       IER - RESULTING ERROR PARAMETER
C           IER=0   NO ERROR
C           IER=1   DNU .LT. 0.0, NO MACH NUMBER SOLUTION, XM=1.0
C           IER=2   DNU .GT. 130 DEG.,  NO NUMERICAL SOLUTION, XM=1000.
C           IER=3   ITERATION DID NOT CONVERGE,DIFFERENCE BETWEEN
C                        CALCULATED AND REQUESTED DNU .GT. 2.0E-6 DEG.
C
C     METHOD -
C         THE EQUATION FOR THE PRANDTL-MEYER EXPANSION ANGLE, FROM
C         NACA REPT. 1135, IS - DNU = SQR6*ATAN(B/SQR6)-ATAN(B), WHERE
C         B = SQRT(XM**2 - 1), AND SQR6 = SQRT(6) = 2.44948974. FOR
C         PROGRAM OPERATIONS DNU IS CONVERTED FROM DEGREES TO RADIANS
C         AND NAMED RNU. IN NEWTONS METHOD, THE EQUATION FOR F(B)
C         IS DEFINED AND BY ITERATION SOLVED FOR B SUCH THAT F(B)=0.0.
C         THE ITERATION FORM IS - B(I+1) = B(I) -F/(DF/DB), WHERE
C              F = F(B) -THE INCREMENT BETWEEN THE PRANDTL-MEYER
C                         ANGLE FOR B(I) AND RNU
C                = SQR6*ATAN(B/SQR6) - ATAN(B) -RNU
C
C              DF/DB - THE FIRST DERIVATIVE OF F(B) WITH RESPECT
C                      TO B
C                    = (5*B**2)/((1+B**2)*(6+B**2))
C
      COMMON /CONSNT/ PI,DEG,UNUSED,DR
      DATA EPS,SQR6 / 2.0E-6, 2.44948974 /
      IER=0
      IF(DNU .LT. 0.0) GO TO 1020
      IF(DNU .EQ. 0.0) GO TO 1030
      IF(DNU.GT.130.) GO TO 1040
      RNU=DNU/DR
      B=1.0
      DO 1000 I=1,30
         F=SQR6*ATAN(B/SQR6)-ATAN(B)-RNU
         IF(ABS(F).LE.EPS) GO TO 1010
         DB=F*(1.+B*B)*(6.+B*B)/(5.*B*B)
         B=B-DB
 1000 CONTINUE
      IER=3
 1010 XM=SQRT(B*B+1.0)
      RETURN
 1020 IER=1
 1030 XM=1.0
      RETURN
 1040 IER=2
      XM=1000.
      RETURN
      END
