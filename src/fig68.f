      SUBROUTINE FIG68 (XM, DELT, THETA, IER)
C
C     THIS SUBROUTINE TAKES THE FREE STREAM MACN NUMBER (XM) AND WEDGE
C     ANGLE (DELT) AND SOLVES THE CUBIC EQUATION FOR SHOCK ANGLE
C     (THETA). THE CUBIC EQUATION FROM NACA REPT. 1135 IS -
C         X**3+P*X**2+Q*X+R=0.0, WHERE X=SIN(THETA)**2.
C
C     DEFINITION OF PARAMETERS -
C         XM    - FREE STREAM MACH NUMBER
C         DELT  - WEDGE ANGLE, IN DEGREES
C         THETA - SHOCK ANGLE, IN DEGREES
C         IER   - ERROR INDICATOR
C               IER=0 NO ERROR
C               IER=1 DELT .LT. 0.0, THETA = MACH ANGLE
C               IER=2 DETACHED SHOCK, THETA = MAX. WEDGE ANGLE FOR
C                                             ATTACHED SHOCK
C               IER=3 XM .LT. 1.0, THETA = 0.0
C
C     METHOD -
C         A CUBIC EQUATION, X**3 + P*X**2 + Q*X + R = 0.0, MAY BE
C         REDUCED TO THE FORM - Y**3 + A*Y + B =0.0, BY SUBSTITUTING
C         FOR X THE VALUE Y-P/3. HERE A = (3*Q - P**2)/3 AND
C         B = (2*P**3 - 9*P*Q + 27*R)/27. FOR ATTACHED SHOCKS AND
C         POSITIVE WEDGE ANGLES THERE ARE THREE REAL AND UNEQUAL
C         ROOTS. IN THIS CASE A TRIGOMETRIC SOLUTION CAN BE USED.
C         FIRST THE ANGLE PHI IS COMPUTED USING THE EXPRESSION -
C         COS(PHI) = -(B/2)/SQRT(-A**3/27), THEN Y WILL HAVE THE
C         FOLLOWING VALUES - Y1=C*COS(PHI/3), Y2=C*COS(PHI/3+120 DEG),
C         AND Y3=C*COS(PHI/3+240 DEG), WHERE C = 2*SQRT(-A/3). THE
C         THREE VALUES OF X ARE - XI = YI-P/3 (I=1,2,3).
C
C         THE INTERPRETATIONS OF THE THREE ROOTS ARE -
C              X1 - CORRESPONDS TO DECREASING ENTROPY, DISREGARD,
C              X2 - STRONG SHOCK, DISREGARD,
C              X3 - WEAK SHOCK, DESIRED ANSWER,
C                 = C*COS(PHI/3 + 240) - P/3.
C                 = SIN(THETA)**2
C
      COMMON /CONSNT/ PI,DEG,UNUSED,DR
      EQUIVALENCE (TMIN,P), (TMAX,S2D,Q,COSP), (DMAX,C2D,R,B,PHI3)
      EQUIVALENCE (XM2,A,S2T)
      IF(XM .LT. 1.0) GO TO 1000
      IER = 0
      DMAX = 0.0
      TMIN = DR*ARCSIN(1./XM)
      IF(XM .EQ. 1.0 .AND. DELT .EQ. 0.0) GO TO 1020
      IF(XM .EQ. 1.0 .AND. DELT .GT. 0.0) GO TO 1030
      IF(DELT .LT. 0.0) GO TO 1010
      IF(DELT .EQ. 0.0) GO TO 1020
      XM2 = XM*XM
      TMAX = (3.*XM2-5.+SQRT(9.*XM2*XM2+12.*XM2+60.))/(7.*XM2)
      DMAX = 1.2*XM2/(XM2*TMAX-1.)-1.
      TMAX = ARCSIN(SQRT(TMAX))
      DMAX = TAN(TMAX)*DMAX
      DMAX = ATAN(1./DMAX)*DR
      TMAX = TMAX*DR
      IF(DELT .GT. DMAX) GO TO 1030
      IF(DELT .EQ. DMAX) GO TO 1040
      S2D = SIN(DELT/DR)
      S2D = S2D*S2D
      C2D = 1.-S2D
      P = -(XM2+2.)/XM2-1.4*S2D
      Q = (2.*XM2+1.)/(XM2*XM2)+(1.44+0.4/XM2)*S2D
      R = -C2D/(XM2*XM2)
      A = Q-P*P/3.
      B = (2.*P*P*P-9.*P*Q+27.*R)/27.
      COSP = -B/(2.*SQRT(-A*A*A/27.))
      PHI3 = ARCCOS(COSP)/3.
      S2T = 2.*SQRT(-A/3.)*COS(PHI3+240./DR)-P/3.
      THETA = ARCSIN(SQRT(S2T))*DR
      RETURN
 1000 IER = 3
      THETA = 0.0
      RETURN
 1010 IER = 1
 1020 THETA = TMIN
      RETURN
 1030 IER = 2
      THETA = DMAX
      RETURN
 1040 THETA = TMAX
      RETURN
      END
