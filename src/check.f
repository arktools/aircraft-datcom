      SUBROUTINE CHECK
C
C      CHECK MACH REGIME LIMITS AND SET PRINT FLAGS
C
C***  IDEAL OUTPUT MATRIX
C             BLOCK    PRINT     IOM
C             NAME     FLAG     ARRAY
C
      COMMON /IBODY/   PBODY,  BODY(400)
      COMMON /IWING/   PWING,  WING(400)
      COMMON /IHT/     PHT,    HT(380)
      COMMON /IVT/     PVT,    VT(380)
      COMMON /IVF/     PVF,    VF(380)
      COMMON /IBW/     PBW,    BW(380)
      COMMON /IBH/     PBH,    BH(380)
      COMMON /IBV/     PBV,    BV(380)
      COMMON /IBWH/    PBWH,   BWH(380)
      COMMON /IBWV/    PBWV,   BWV(380)
      COMMON /IBWHV/   PBWHV,  BWHV(380)
      COMMON /IPOWER/  PPOWER, POWER(200)
      COMMON /IDWASH/  PDWASH, DWASHI(60)
C
      LOGICAL PBODY, PWING, PHT, PVT, PVF, PBW, PBH, PBV, PBWH, PBWV,
     1        PBWHV, PPOWER, PDWASH
C
C***  INPUT DATA BLOCKS
C
      COMMON /FLGTCD/ FLC(160)
      COMMON /OPTION/ SREF,CBARR,ROUGFC,BLREF
      COMMON /SYNTSS/ XCG, XW, ZW, ALIW, ZCG, XH, ZH, ALIH, XV,
     1                VERTUP, HINAX, XVF, SCALE, ZV, ZVF, YV, YF,
     2                PHIV, PHIF
      COMMON /BODYI/  XNX,X(20),S(20),P(20),R(20),ZU(20),ZL(20),
     1                BNOSE,BTAIL,BLN,BLA,DS,ITYPE,METHOD,ELLIP
      COMMON /WINGI/  WGIN(101)
      COMMON /VTI/    VTIN(154), TVTIN(8), VFIN(154)
      COMMON /HTI/    HTIN(154)
      COMMON /POWER/  PWIN(29), LBIN(21)
      COMMON /FLAPIN/ F(138)
C
      EQUIVALENCE (F(14),SPANI), (DWASH,WGIN(101))
      LOGICAL FLP,FLAG,VERTUP
      LOGICAL IMACH, IRN, IVEL, IALT, IPT, IATM
      REAL METHOD
C
C***  REFERENCE DATA BLOCKS
C
      COMMON /CASEID/ ID(175), IDIM
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD
      COMMON /FLOLOG/ FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1                HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2                TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3                HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4                VFPL,VFSC,CTAB,PLOT
      COMMON /ERROR/  IERR,GONOGO,IEND,DMPALL,DPB,DPA,DPBD,DPAVF,
     1                DPFACT,DPWBT,DPBHT,DPAVT,DPAHT,DPC,DPD,DPWB,
     2                DPCHT,DPDHT,DPDYNH,SAVE,DMPCSE,DPDVT,DPGR,DPLB,
     3                DPPW,DPSTB,DPSBD,DPSLG,DPSWB,DPSTP,DPDWA,DPSTG,
     4                DPSLA,DPTRA,DPEXPD,DPDVF,DPFLP,DPFHG,DPFCM,DPTCD,
     5                DPFLA,DPTRM,DPSPR,DPTRN,DPTRM2,DPHYP,DPDYN,DPJET,
     6                DPHB,DPSHB,DPTRAH,DPSTBH,DPSEC,DPSLAH,DPINPT,
     7                DPFLC,DPOPTN,DPSYN,DPBDIN,DPWGIN,DPVTIN,DPTVT,
     8                DPVFIN,DPHTIN,DPPWIN,DPLBIN,DPF,DPIOM,
     9                DPIBDY,DPIWG,DPIHT,DPIVT,DPIVF,DPIBW,DPIBH,DPIBV,
     A                DPIBWH,DPIBWV,DPITOT,DPIPWR,DPIDWH
C
      LOGICAL  FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1         HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2         TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3         HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4         VFPL,VFSC,CTAB,PLOT
      LOGICAL  IERR,GONOGO,IEND,DMPALL,DPB,DPA,DPBD,DPAVF,
     1         DPFACT,DPWBT,DPBHT,DPAVT,DPAHT,DPC,DPD,DPWB,
     2         DPCHT,DPDHT,DPDYNH,SAVE,DMPCSE,DPDVT,DPGR,DPLB,
     3         DPPW,DPSTB,DPSBD,DPSLG,DPSWB,DPSTP,DPDWA,DPSTG,
     4         DPSLA,DPTRA,DPEXPD,DPDVF,DPFLP,DPFHG,DPFCM,DPTCD,
     5         DPFLA,DPTRM,DPSPR,DPTRN,DPTRM2,DPHYP,DPDYN,DPJET,
     6         DPHB,DPSHB,DPTRAH,DPSTBH,DPSEC,DPSLAH,DPINPT,
     7         DPFLC,DPOPTN,DPSYN,DPBDIN,DPWGIN,DPVTIN,DPTVT,
     8         DPVFIN,DPHTIN,DPPWIN,DPLBIN,DPF,DPIOM,
     9         DPIBDY,DPIWG,DPIHT,DPIVT,DPIVF,DPIBW,DPIBH,DPIBV,
     A         DPIBWH,DPIBWV,DPITOT,DPIPWR,DPIDWH
C
C***  CHECK BODY METHOD, ASPECT RATIO CLASSIFICATION, AND DOWNWASH
C
      IF(METHOD .LE. 1.5) METHOD = 1.0
      IF(METHOD .GT. 1.5) METHOD = 2.0
C
      IF(WGIN(92) .EQ. UNUSED) WGIN(92) = 3.5
      IF(WGIN(92) .LT. 3.0)    WGIN(92) = 3.0
      IF(WGIN(92) .GT. 4.0)    WGIN(92) = 4.0
      IF(HTIN(92) .EQ. UNUSED) HTIN(92) = 3.5
      IF(HTIN(92) .LT. 3.0)    HTIN(92) = 3.0
      IF(HTIN(92) .GT. 4.0)    HTIN(92) = 4.0
      IF(VTIN(92) .EQ. UNUSED) VTIN(92) = 3.5
      IF(VTIN(92) .LT. 3.0)    VTIN(92) = 3.0
      IF(VTIN(92) .GT. 4.0)    VTIN(92) = 4.0
      IF(VFIN(92) .EQ. UNUSED) VFIN(92) = 3.5
      IF(VFIN(92) .LT. 3.0)    VFIN(92) = 3.0
      IF(VFIN(92) .GT. 4.0)    VFIN(92) = 4.0
C
      IF(.NOT. (WGPL .AND. HTPL)) GO TO 1000
        SPANR  = WGIN(4)/HTIN(4)
        IF((DWASH .EQ. UNUSED) .AND. (SPANR .LT. 1.5)) DWASH = 3.0
        IF(DWASH .LE. 1.5) DWASH = 1.0
        IF(DWASH .GE. 2.5) DWASH = 3.0
        IDWASH = DWASH+0.5
        IF(IDWASH .EQ. 2 .AND. SPANR .LT. 1.25) IDWASH = 3
        IF(IDWASH .EQ. 2 .AND. SPANR .GT. 3.60) IDWASH = 1
        IF(IDWASH .EQ. 1 .AND. SPANR .LE. 1.00) IDWASH = 3
        IF(IDWASH .EQ. 3 .AND. SPANR .GT. 1.50) IDWASH = 1
        DWASH = IDWASH
 1000 CONTINUE
C
C***  SET IOM PRINT FLAGS
C
      IF(BO .AND. .NOT. BUILD) GO TO 1010
        IF(BO)   PBODY = .TRUE.
        IF(WGPL) PWING = .TRUE.
        IF(HTPL)   PHT = .TRUE.
        IF(VFPL)   PVF = .TRUE.
        IF((VTPL .OR. TVTPAN))   PVT = .TRUE.
 1010 CONTINUE
      IF(.NOT. BO) GO TO 1040
        IF(WGPL .AND. .NOT. BUILD) GO TO 1020
          IF(WGPL) PBW = .TRUE.
          IF(HTPL) PBH = .TRUE.
          IF((VTPL .OR. TVTPAN)) PBV = .TRUE.
          IF(WGPL .AND. HTPL) PBWH = .TRUE.
          IF(WGPL .AND. (VTPL .OR. TVTPAN)) PBWV = .TRUE.
          IF(.NOT. HTPL .AND. .NOT. (VTPL .OR. TVTPAN)) PBODY = .TRUE.
 1020     CONTINUE
        IF(.NOT. WGPL) GO TO 1030
          IF(HTPL .AND. (VTPL .OR. TVTPAN)) PBWHV = .TRUE.
          IF(.NOT. HTPL .AND. (VTPL .OR. TVTPAN)) PBWV = .TRUE.
          IF(.NOT. (VTPL .OR. TVTPAN) .AND. HTPL) PBWH = .TRUE.
          IF(.NOT. HTPL .AND. .NOT. (VTPL .OR. TVTPAN)) PBW = .TRUE.
 1030     CONTINUE
          IF(WGPL .AND. HTPL) PDWASH = .TRUE.
          IF(PRPOWR .OR. JETPOW) PPOWER = .TRUE.
 1040 CONTINUE
      IF(LOASRT) PBW = .TRUE.
C
C***  CHECK FLAP INBOARD STATION
C
      FLP = SYMFP .OR. ASYFP
      IF(.NOT. FLP) GO TO 1070
      IF(ASYFP .AND. (ABS(F(18)-5.) .LE. 0.1)) GO TO 1070
      SSPN  = WGIN(4)
      SSPNE = WGIN(3)
      IF(.NOT. HTPL) GO TO 1050
          SSPN  = HTIN(4)
          SSPNE = HTIN(3)
 1050 CONTINUE
      SBOD = SSPN-SSPNE
      IF(SPANI .GE. SBOD) GO TO 1070
          WRITE(6,1060) SPANI, SBOD
 1060     FORMAT(1H0,35HERROR ** FLAP INBOARD SPAN, SPANI =,E12.5,
     1      52H, IT IS INSIDE THE BODY AS DEFINED BY SSPN AND SSPNE /
     2      10X,40HSPANI IS REDEFINED, SPANI = SSPN-SSPNE =,E12.5/)
          SPANI = SBOD
 1070 CONTINUE
C
C***  CHECK FOR MISSING ESSENTIAL NAMELISTS
C
      FLAG = LOASRT .OR. HYPEF .OR. TRAJET
      IF(.NOT. FLAG) CALL MAJERR
C
C***  TEST INPUT UNITS, NOSE LOCATION, AND SCALING
C***  TEST FLIGHT CONDITION INPUTS
C
      IF(SCALE .LE. UNUSED) SCALE = 1.0
      CALL CONV(IDIM,SCALE)
C
      IF(FLC(160) .LT. 1.0) FLC(160) = 1.0
      IF(FLC(160) .GT. 3.0) FLC(160) = 3.0
      LOOP  = FLC(160)+0.5
      NMACH = FLC(1)  +0.5
      NALT  = FLC(159)+0.5
      IMACH = FLC(3)   .NE. UNUSED
      IRN   = FLC(43)  .NE. UNUSED
      IVEL  = FLC(137) .NE. UNUSED
      IALT  = FLC(97)  .NE. UNUSED
      IPT   =(FLC(117) .NE. UNUSED) .AND. (FLC(74) .NE. UNUSED)
      IATM  = IALT .OR. IPT
C
      IF(IMACH) GO TO 1130
        IF(IVEL) GO TO 1090
          WRITE(6,1080)
 1080     FORMAT(1H0,41H*** ERROR *** NO SPEED REFERENCE IS INPUT,
     1               27H MACH(1) = VINF(1) = UNUSED/
     2               15X,31HTHE NEXT CASE WILL BE ATTEMPTED/)
          IERR = .TRUE.
 1090   CONTINUE
        IF(.NOT. IVEL) GO TO 1120
        IF(IATM)       GO TO 1120
          WRITE(6,1100)
 1100     FORMAT(1H0,35H*** ERROR *** VELOCITY IS INPUT AND,
     1               43H ATMOSPHERIC DATA ARE NOT DEFINED (ALT(1) = ,
     2               27HPINF(1) = TINF(1) = UNUSED)/
     3               15X,41HMACH WILL BE CALCULATED USING A SPEED-OF-,
     4               17HSOUND OF 1000 FPS/)
          DO 1110 I=1,NMACH
            FLC(I+2) = FLC(I+136)/1000.0
 1110     CONTINUE
 1120   CONTINUE
 1130 CONTINUE
      IF(IRN .OR. IATM) GO TO 1160
        WRITE(6,1140)
 1140   FORMAT(1H0,43H*** ERROR *** REYNOLDS NUMBER IS NOT INPUT ,
     1             46HAND ATMOSPHERIC DATA ARE NOT DEFINED (ALT(1) =,
     2             28H PINF(1) = TINF(1) = UNUSED)/
     3             15X,38HREYNOLDS NUMBER WILL BE SET TO 5*10**6/)
        DO 1150 I=1,NMACH
          FLC(I+42) = 5.0E6
 1150   CONTINUE
 1160 CONTINUE
C
C***  CHECK PROGRAM LOOPING
C
      FLAG = (LOOP .EQ. 1) .AND. IATM
      IF(.NOT. FLAG) GO TO 1190
        IF(FLC(159) .EQ. UNUSED) NALT = NMACH
        IF(NALT .EQ. NMACH) GO TO 1180
          I = MIN0(NALT,NMACH)
          WRITE(6,1170) NMACH, NALT, I
 1170     FORMAT(1H0,39H*** ERROR *** LOOP = 1 AND NALT IS NOT ,
     1               23HEQUAL TO NMACH (NALT = ,I2,10H, NMACH = ,
     2               I2,1H)/15X,31HNMACH AND NALT ARE BOTH SET TO ,
     3               I2/)
          NMACH = I
          NALT  = I
 1180   CONTINUE
 1190 CONTINUE
C
      FLAG = (LOOP .GT. 1) .AND. (.NOT. IATM)
      IF(.NOT. FLAG) GO TO 1210
        WRITE(6,1200) LOOP
 1200   FORMAT(1H0,21H*** ERROR *** LOOP = ,I2,16H AND ATMOSPHERIC ,
     1             34HDATA ARE NOT DEFINED, SET LOOP = 1/)
        LOOP = 1
 1210 CONTINUE
      FLC(160) = LOOP
      FLC(1)   = NMACH
      FLC(159) = NALT
C
C***  CALCULATE ATMOSPHERIC PRESSURE AND TEMPERATURE
C
      IF(.NOT. IALT) GO TO 1230
      IF(IPT)        GO TO 1230
        DO 1220 I=1,NALT
          IF(ABS(FLC(I+96)) .LE. UNUSED) FLC(I+96) = 0.0
          CALL ATMOS(FLC(I+96), FLC(I+116), FLC(I+73))
 1220   CONTINUE
 1230 CONTINUE
C
C***  WRITE ARRAY FLC FOR PLOTTING
C
      IF(PLOT) WRITE(13,1240) FLC
 1240 FORMAT(1P,10E12.4)
C
      RETURN
      END
