      SUBROUTINE JETFP
C
C  CALCULATE JET FLAP AERODYNAMIC INCREMENTS
C
      COMMON /IWING/  PWING,WING(400)
      COMMON /POWER/  P(12),PWIN(26)
      COMMON /OPTION/ SREF,CBARR
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD
      COMMON /SYNTSS/ XCG,XW,ZW,ALIW,ZCG,XH,ZH,ALIH,XV,VERTUP,HINAX,
     1                XVF,SCALE,ZV,ZVF,YV,YF,PHIV,PHIF
      COMMON /FLAPIN/ F(84)
      COMMON /WINGD/  A(195)
      COMMON /WINGI/  WINGIN(100)
C
      LOGICAL VERTUP
      DIMENSION DELFLP(10),CPRMEI(10),CPRMEO(10),DELJET(10),
     1          X149(18),Y149(63),Q149(3),X418(24),Y418(48),Q418(3),
     2          X409(22),Y409(110),Q409(3),X415(22),Y415(33),Q415(3),
     3          X412(7),Y412(7),Q412(3),ROUT(2),CC(6),DECCL(10),
     4          EFFJET(10),DCLMAX(10),CLAUB(10),CLAB(10),VAR(3),LEN(3),
     5    DELCM(10),X237(22),Y237(88),Q237(3),X568(22),Y568(121),Q568(3)
C
      EQUIVALENCE     (SW,A(4)),(TANLE,A(38)),(DCLMAX(1),WING(221)),
     1 (CHRDFI,F(12)),(CHRDFO,F(13)),(SPANFI,F(14)),(TANTE,A(56)),
     2 (SPANFO,F(15)),(CMU,F(63)),(AIETLJ,PWIN(1)),(JEALOC,PWIN(6)),
     3 (JEANGL,PWIN(8)),(JELLOC,PWIN(12)),(JERAD,PWIN(15)),
     4 (DELFLP(1),F(1)),(CPRMEI(1),F(39)),(CPRMEO(1),F(49)),
     5 (DELJET(1),F(64)),(CLAUB(1),WING(101)),(SSPN,WINGIN(4)),
     6 (CHRDR,WINGIN(6)),(DECCL(1),WING(201)),(EFFJET(1),F(75)),
     7 (TAPR,A(118)),(JEVLOC,PWIN(5)),(AR,A(120)),(TWISTA,WINGIN(11)),
     8 (TOC,WINGIN(16)),(DELCM(1),WING(211)),(CLAB(1),WING(241)),
     9 (SSPNE,WINGIN(3)),(CHRDTP,WINGIN(1))
C
      INTEGER FTYPE
      REAL    JEALOC, JEANGL, JELLOC, JERAD, JEVLOC
C
C***** DATA FOR FIG. 6.1.1.1-49
C
      DATA X149 /0.,.5,1.,2.,3.,4.,6.,8.,10.,0.,.05,.1,.2,.3,.5,1.,2*0./
      DATA Y149 /0.,2.8,4.,6.,7.9,9.7,12.8,15.6,18.4,1.9,3.5,4.8,6.8,
     1 8.5,10.2,13.2,16.1,19.,2.3,3.9,5.2,7.2,8.9,10.6,13.6,16.5,19.4,
     2 3.5,5.,6.,8.,9.6,11.3,14.2,17.3,20.,4.2,5.5,6.5,8.5,10.2,11.9,
     3 14.6,17.8,20.5,5.1,6.2,7.5,9.1,10.3,12.6,15.6,18.6,21.1,6.3,
     4 7.5,8.5,10.5,12.1,13.6,16.5,19.2,22.1/
      DATA Q149 / 4H6.1., 4H1.1-, 4H49  /
C
C***** DATA FOR FIG. 6.1.4.1-18
C
      DATA X418 /0.,.5,1.,2.,3.,4.,5.,6.,7.,8.,9.,10.,.1,.2,.3,.4,8*0./
      DATA Y418 /0.,1.7,2.8,4.8,6.5,8.1,9.75,11.1,12.6,14.05,15.5,16.9,
     1 0.,1.5,2.6,4.5,6.2,7.85,9.3,10.9,12.25,13.8,15.1,16.5,
     2 0.,1.45,2.45,4.3,6.05,7.75,9.1,10.75,12.05,13.55,14.98,16.4,
     3 0.,1.45,2.45,4.25,5.95,7.55,9.,10.55,11.95,13.25,14.8,16.05/
      DATA Q418 / 4H6.1., 4H4.1-, 4H18  /
C
C***** DATA FOR FIG. 6.1.4.2-9
C
      DATA X409 /0.,1.,2.,3.,4.,5.,6.,7.,8.,9.,10.,1.,2.,3.,4.,5.,6.,7.,
     1 8.,9.,10.,0./
      DATA Y409 /1.,1.52,2.02,2.49,2.9,3.35,3.75,4.15,4.55,4.95,5.35,
     1 1.,1.35,1.65,1.95,2.21,2.5,2.75,3.03,3.28,3.56,3.8,1.,1.3,1.56,
     2 1.8,2.03,2.24,2.46,2.7,2.9,3.13,3.35,1.,1.3,1.53,1.75,1.95,2.16,
     3 2.35,2.55,2.75,2.95,3.15,1.,1.3,1.53,1.73,1.93,2.12,2.3,2.5,
     4 2.67,2.84,3.07,1.,1.3,1.53,1.73,1.92,2.1,2.28,2.46,2.65,2.82,
     5 3.03,1.,1.3,1.53,1.73,1.92,2.1,2.28,2.45,2.63,2.8,2.98,1.,1.3,
     6 1.53,1.73,1.92,2.1,2.28,2.45,2.63,2.8,2.98,1.,1.3,1.53,1.73,
     7 1.92,2.1,2.28,2.45,2.63,2.8,2.98,1.,1.3,1.53,1.73,1.92,2.1,
     8 2.28,2.45,2.63,2.8,2.98/
      DATA Q409 / 4H6.1., 4H4.2-, 4H9   /
C
C***** DATA FOR FIG. 6.1.4.1-15
C
      DATA X415 /0.,.1,.2,.3,.4,.5,.6,.7,.8,.9,1.,0.,.5,1.,8*0./
      DATA Y415 /0.,.17,.3,.43,.57,.67,.78,.86,.93,.98,1.,0.,.14,.28,
     1.4,.52,.52,.73,.82,.91,.97,1.,0.,.13,.26,.38,.39,.5,.7,.8,.89,.96,
     2 1./
      DATA Q415 / 4H6.1., 4H4.1-, 4H15  /
C
C***** DATA FOR FIG. 6.1.4.3-12
C
      DATA X412 /0.,.4,.8,1.2,1.6,2.,2.4/
      DATA Y412 /0.,1.6,2.65,3.65,4.55,5.4,6.2/
      DATA Q412 / 4H6.1., 4H4.3-, 4H12  /
C
C***** DATA FOR FIG. 6.1.2.1-37
C
      DATA X237 /0.,1.,2.,3.,4.,5.,6.,7.,8.,9.,10.,0.,.05,.1,.2,
     1 .3,.5,.7,1.,3*0./
      DATA Y237 /.5,.575,.61,.63,.655,.67,.685,.695,.705,.71,.715,
     1 .49,.55,.58,.605,.62,.645,.66,.67,.675,.68,.69,.48,.525,
     2 .56,.585,.6,.61,.62,.635,.64,.65,.655,.445,.485,.51,.54,
     3 .555,.565,.575,.585,.59,.595,.599,.42,.45,.475,.49,.5,
     4 .505,.51,.515,.52,.525,.53,.38,.385,.39,.399,.402,.405,.41,
     5 .415,.42,.422,.426,.325,.32,9*.315,.25,.23,.215,.21,.202,
     6 .198,.195,.19,.185,.178,.175/
      DATA Q237 / 4H6.1., 4H2.1-, 4H37  /
C
C***** DATA FOR FIG. 6.1.5.1-68
C
      DATA X568 /0.,.1,.2,.3,.4,.5,.6,.7,.8,.9,1.,0.,.05,.1,.15,
     1 .2,.25,.3,.35,.4,.45,.5/
      DATA Y568 /1.,1.098,1.18,1.255,1.32,1.38,1.43,1.48,1.52,1.575,
     1 1.605,1.,1.095,1.175,1.245,1.31,1.375,1.415,1.46,1.5,1.55,
     2 1.58,1.,1.09,1.17,1.235,1.3,1.365,1.4,1.445,1.48,1.52,1.57,
     3 1.,1.085,1.16,1.225,1.29,1.345,1.385,1.43,1.46,1.5,1.54,
     4 1.,1.08,1.15,1.215,1.28,1.325,1.375,1.4,1.445,1.48,1.505,
     5 1.,1.075,1.14,1.205,1.26,1.305,1.355,1.385,1.42,1.455,1.48,
     6 1.,1.07,1.13,1.195,1.24,1.29,1.33,1.36,1.39,1.425,1.46,
     7 1.,1.065,1.12,1.18,1.22,1.265,1.3,1.345,1.37,1.39,1.42,
     8 1.,1.06,1.11,1.16,1.2,1.24,1.28,1.3,1.33,1.36,1.38,
     9 1.,1.055,1.1,1.145,1.18,1.21,1.245,1.27,1.29,1.315,1.34,
     A 1.,1.05,1.09,1.12,1.16,1.185,1.205,1.23,1.25,1.275,1.29/
      DATA Q568 / 4H6.1., 4H5.1-, 4H68  /
C
      DATA ROUT / 4HJETF, 4HP   /
C
C---- CALCULATE THE AVERAGE FLAP CHORD FOR THE WING
C
      IF(CMU .EQ. UNUSED .OR. F(74) .EQ. UNUSED) GO TO 1100
      CF=(CHRDFI+CHRDFO)/2.
      IF(JEANGL .EQ. UNUSED) JEANGL=12.0
      TANJET=TAN(JEANGL*DEG)
C
C---- CALCULATE AVERAGE BASIC WING CHORD IN BLOWN REGION
C
      C=CHRDR+(SPANFI+SPANFO)*(CHRDTP-CHRDR)/(2.0*SSPN)
      IN=0
      JETFLP=F(74)+.5
      NDELTA=F(16)+.5
      FTYPE=F(17)+.5
      DO 1090 N=1,NDELTA
C
C  CALCULATE SECTION GEOMETRIC CONSTANTS
C
C---- GET AVERAGE C-PRIME FOR WING
C
         CPRIME=(CPRMEI(N)+CPRMEO(N))/2.
         IF(FTYPE .EQ. 5 .OR. FTYPE .EQ. 1 .OR. FTYPE .EQ. 6) CPRIME=C
C
C---- CALCULATE AVERAGE WING EXTENDED CHORD IN BLOWN REGION
C---- AIRFOIL THICKNESS RATIO BASED ON EXTENDED CHORD (C-PRIME)
C---- GENERAL AIRFOIL USED FOR CONSTANT KT (KT=0.80)
C
         TCPRM=TOC*C/CPRIME*0.80
C
C---- CALCULATE TOTAL WING AREA INCLUDING FLAP
C
         ST=SW+2.0*(CPRIME-C)*(SPANFO-SPANFI)
         IF(FTYPE .EQ. 5 .OR. FTYPE .EQ. 1 .OR. FTYPE .EQ. 6) ST=SW
C
C---- CALCULATE TOTAL WING-FLAP COMBINATION ASPECT RATIO
C
         AT=AR*SW/ST
C
C---- CALCULATE FLAP T.E. FOR BLOWING BOUNDARIES
C
         YI=SPANFI
         YO=SPANFO
         IF(JETFLP .NE. 3) GO TO 1000
         COSDF=COS(DELFLP(N)*DEG)-1.
         BK=(CPRMEI(N)-CPRMEO(N))/(SPANFI-SPANFO)
         FK=(CHRDFI-CHRDFO)/(SPANFI-SPANFO)
         YI=(JELLOC-JERAD-TANJET*(XW-JEALOC+CPRMEO(N)-BK*SPANFO
     1      -FK*COSDF*SPANFO+CHRDFO))/(1.+TANJET*(TANLE-BK-FK*COSDF))
         YO=2.*JELLOC-YI
         IF(YI .LT. SPANFI) YI=SPANFI
         IF(YO .GT. SPANFO) YO=SPANFO
C
C---- CALCULATE BASIC WING BLOWN AREA
C
 1000    SJ=(YO-YI)*CHRDR*(2.-(YI+YO)*(1.-TAPR)/SSPN)
C
C  CALCULATE SECTION LIFT USING CMU-PRIME
C
         IF(JETFLP .EQ. 2 .OR. JETFLP .EQ. 3) GO TO 1010
C
C---- HERE FOR PURE JET FLAP OR COMBINATION
C---- FIG. 6.1.1.1-49, CLDJ
C
         VAR(1)=CMU*C/CPRIME
         VAR(2)=0.0
         LEN(1)=9
         LEN(2)=7
         CALL INTERX(2,X149,VAR,LEN,Y149,CLDJ,9,63,1,1,0,0,1,1,0,0,
     1               Q149,3,ROUT)
         DELCL=((1.+TCPRM)*DELJET(N)*(CLDJ-CMU*C/CPRIME)+
     1         CMU*C/CPRIME*DELJET(N))*CPRIME/(C*RAD)
         IF(JETFLP .EQ. 4) DELCL1=DELCL
         IF(JETFLP .EQ. 1) GO TO 1020
 1010    CONTINUE
C
C---- HERE FOR IBF, EBF OR COMBINATION
C---- FIG. 6.1.1.1-49, CLDF
C
         VAR(1)=CMU*C/CPRIME
         VAR(2)=CF/CPRIME
         LEN(1)=9
         LEN(2)=7
         CALL INTERX(2,X149,VAR,LEN,Y149,CLDF,9,63,1,1,0,0,1,1,0,0,
     1               Q149,3,ROUT)
         DELCL=((1.+TCPRM)*DELFLP(N)*(CLDF-CMU*C/CPRIME)+
     1         CMU*C/CPRIME*DELFLP(N))*CPRIME/(C*RAD)
         IF(JETFLP .EQ. 4)DELCL=DELCL+DELCL1
 1020    CONTINUE
C
C  CALCULATE THREE DIMENSIONAL JET MOMENTUM COEFFICIENT
C
         CJ=2./SREF*C*CMU*(SPANFO-SPANFI)
         CJPRM=CJ*SREF/SJ
C
C  CALCULATE THE SECTION LIFT USING CJ-PRIME
C
         IF(JETFLP .EQ. 2 .OR. JETFLP .EQ. 3) GO TO 1030
         VAR(1)=CJPRM
         VAR(2)=0.0
         LEN(1)=9
         LEN(2)=7
         CALL INTERX(2,X149,VAR,LEN,Y149,CLDJT,9,63,1,1,0,0,1,1,0,0,
     1               Q149,3,ROUT)
         DELCLT=((1.+TCPRM)*DELJET(N)*(CLDJT-CJPRM)+CJPRM*DELJET(N))*
     1          CPRIME/(C*RAD)
         IF(JETFLP .EQ. 4)DELCL2=DELCLT
         IF(JETFLP .EQ. 1) GO TO 1040
 1030    VAR(1)=CJPRM
         VAR(2)=CF/CPRIME
         LEN(1)=9
         LEN(2)=7
         CALL INTERX(2,X149,VAR,LEN,Y149,CLDFT,9,63,1,1,0,0,1,1,0,0,
     1               Q149,3,ROUT)
         DELCLT=((1.+TCPRM)*DELFLP(N)*(CLDFT-CJPRM)+CJPRM*DELFLP(N))
     1          *CPRIME/(C*RAD)
         IF(JETFLP .EQ. 4) DELCLT=DELCLT+DELCL2
 1040 CONTINUE
C
C---- EQN. 6.1.4.1-B, IBF
C---- IF CONFIGURATION IS PURE JET FLAP, USE IBF EQN TO APPROXIMATE
C
         IF(JETFLP .NE. 2 .AND. JETFLP .NE. 1) GO TO 1050
         DECCL(N)=DELCLT*SJ/SREF*(AT+2.*CJPRM/PI)/
     1            (AT+2.+.604*SQRT(CJPRM)+.876*CJPRM)
 1050    CONTINUE
         IF(JETFLP .NE. 3) GO TO 1060
C
C---- HERE FOR EBF
C---- GET TWO DIMENSIONAL LIFT CURVE SLOPE FROM 6.1.1.1
C---- FIG. 6.1.1.1-49
C
         VAR(1)=CJPRM
         VAR(2)=1.
         LEN(1)=9
         LEN(2)=7
         CALL INTERX(2,X149,VAR,LEN,Y149,CLAPRM,9,63,1,1,0,0,1,1,0,0,
     1              Q149,3,ROUT)
C
C---- FIG. 6.1.4.1-18
C
         VAR(1)=CJPRM
         VAR(2)=CF/CPRIME
         LEN(1)=12
         LEN(2)=4
         CALL INTERX(2,X418,VAR,LEN,Y418,PIDO4,12,48,1,1,0,0,1,1,0,0,
     1               Q418,3,ROUT)
C
C---- EQN. 6.1.4.1-C
C
         DECCL(N)=PIDO4*EFFJET(N)*SJ/(SREF*RAD)*(PI*AT+2.*CJPRM)/
     1            (PI*AT+CLAPRM+2.01*CJPRM)
 1060    CONTINUE
C
C  CAN ADD DECCL TO FLAP RETRACTED POWER-OFF DATA FOR TOTAL LIFT
C
         IF(JETFLP .EQ. 4) GO TO 1090
C
C  CALCULATE LIFT-CURVE-SLOPE FROM 6.1.4.2 (PURE JET, IBF OR EBF ONLY)
C
         TERM2=CJ*(COS((EFFJET(N)+DELJET(N))*DEG)-1.)/RAD
C
C---- K(AT,CJPRIME), FIG. 6.1.4.2-9
C
         VAR(1)=CJPRM
         VAR(2)=AT
         LEN(1)=11
         LEN(2)=10
         CALL INTERX(2,X409,VAR,LEN,Y409,ATCJK,11,110,1,1,0,0,1,1,0,0,
     1               Q409,3,ROUT)
C
C---- KB, FIG. 6.1.4.1-15 AND SKETCH (A) OF 6.1.4.1
C
         ETAI=SPANFI/SSPN
         ETAO=SPANFO/SSPN
         VAR(1)=ETAI
         VAR(2)=1.
         LEN(1)=11
         LEN(2)=3
         CALL INTERX(2,X415,VAR,LEN,Y415,BK1,11,33,1,1,0,0,1,1,0,0,
     1               Q415,3,ROUT)
         VAR(1)=ETAO
         CALL INTERX(2,X415,VAR,LEN,Y415,BK2,11,33,1,1,0,0,1,1,0,0,
     1               Q415,3,ROUT)
         BK=BK2-BK1
C
C---- FIRST TERM OF EQN. 6.1.4.2-B
C
         TERM1K=(ATCJK-1.)*BK+1.
C
C  MULTIPLY UNBLOWN FLAP DEFLECTED WING LIFT-CURVE-SLOPE BY
C  TERM1K AND ADD THIS RESULT TO TERM2.  THIS IS THE TOTAL
C  BLOWN WING LIFT-CURVE-SLOPE.
C
         CLAB(N)=TERM1K*CLAUB(N)+TERM2
         IF(JETFLP .NE. 3) GO TO 1070
C
C---- WING MAXIMUM LIFT, SECTION 6.1.4.3 (EBF ONLY)
C---- OMIT CLMAX CALCULATION IF FLAP NOT DOUBLE SLOTTED
C
         ETAT=0.0
         IF(FTYPE .NE. 4) GO TO 1070
C
C---- FIG. 6.1.4.3-11, STATIC TURNING EFFICIENCY
C
         ETAT=1.-2.*EFFJET(N)/300.
         CONS=ETAT*CJ*SIN(EFFJET(N)*DEG)
C
C---- FIG. 6.1.4.3-12
C
         CALL TBFUNX(CONS,DCLMAX(N),DYDX,7,X412,Y412,CC,IN,MI,NG,
     1               1,1,Q412,3,ROUT)
C
C  WING PITCHING MOMENT INCREMENT, SECTION 6.1.5.1
C
C  PURE JET, IBF OR EBF ONLY, NO LEADING EDGE DEVICES
C
 1070    IF(FTYPE .GT. 5) GO TO 1090
         IF(JETFLP .NE. 3) GO TO 1080
C
C---- HERE FOR EBF
C---- CHECK TO MAKE SURE THAT JET BLOWS OVER FLAP
C---- CALCULATE EXHAUST HEIGHT AT WING TRAILING EDGE
C
         EH=(JERAD+TANJET*(CPRIME-CF+XW+JELLOC*TANLE))*COS(AIETLJ*DEG)
         IF(EH .LT. (JEVLOC-ZW)) WRITE(6,1110)
         IF(EH .LT. (JEVLOC-ZW)) GO TO 1100
C
C---- DIVIDE THE WING INTO SECTIONS FOR PITCHING MOMENT ANALYSIS
C---- A TOTAL OF 5 SECTIONS IS TO BE CALCULATED
C
 1080    CONTINUE
         ETA1=SPANFI/SSPN
         ETA2=YI/SSPN
         ETA3=YO/SSPN
         ETA4=SPANFO/SSPN
         ETA5=1.0
         AT=AR*C/CPRIME
         CMUPRM=CJ*SREF/SJ*C/CPRIME
         AK1=0.
         AK2=AT/(2.+AT)*CPRIME/C
         AK3=(AT+2.*CMUPRM/PI)/(AT+2.+.604*SQRT(CMUPRM)+.876*CMUPRM)*
     1       CPRIME/C
         AK4=AK2
         AK5=0.
C
C---- FIG. 6.1.4.1-15
C
         VAR(1)=ETA1
         VAR(2)=TAPR
         LEN(1)=11
         LEN(2)=3
         CALL INTERX(2,X415,VAR,LEN,Y415,BK10,11,33,1,1,0,0,1,1,0,0,
     1               Q415,3,ROUT)
         VAR(1)=ETA2
         CALL INTERX(2,X415,VAR,LEN,Y415,BK22,11,33,1,1,0,0,1,1,0,0,
     1               Q415,3,ROUT)
         BK21=BK22-BK10
         VAR(1)=ETA3
         CALL INTERX(2,X415,VAR,LEN,Y415,BK33,11,33,1,1,0,0,1,1,0,0,
     1               Q415,3,ROUT)
         BK32=BK33-BK22
         VAR(1)=ETA4
         CALL INTERX(2,X415,VAR,LEN,Y415,BK44,11,33,1,1,0,0,1,1,0,0,
     1               Q415,3,ROUT)
         BK43=BK44-BK33
         BK54=1.-BK44
C
C---- STRAIGHT TAPERED WINGS ONLY
C
         IF(TAPR .NE. 1.) XMOC=(1.+TAPR)*AR*TANLE/(4.*(1.-TAPR))
         IF(TAPR .EQ. 1.) XMOC=(XW-XCG)/CHRDR
C
C---- FIG. 6.1.2.1-37, TWO DIMENSIONAL CENTER OF LIFT LOCATION
C
         VAR(1)=CMUPRM
         VAR(2)=CF/CPRIME
         LEN(1)=11
         LEN(2)=8
         CALL INTERX(2,X237,VAR,LEN,Y237,XF2F,11,88,1,1,0,0,1,1,0,0,
     1               Q237,3,ROUT)
C
C---- FIG. 6.1.5.1-68 3-D TO 2-D XCP FRACTION
C
         VAR(1)=1./AT
         VAR(2)=CF/CPRIME
         LEN(1)=11
         LEN(2)=11
         CALL INTERX(2,X568,VAR,LEN,Y568,X3D2F,11,121,1,1,0,0,1,1,0,0,
     1               Q568,3,ROUT)
C
C---- FIG. 6.1.2.1-37, JET CENTER OF LIFT, 2-D
C
         VAR(1)=CMUPRM
         VAR(2)=0.
         LEN(1)=11
         LEN(2)=8
         CALL INTERX(2,X237,VAR,LEN,Y237,XF2J,11,88,1,1,0,0,1,1,0,0,
     1               Q237,3,ROUT)
C
C---- FIG. 6.1.5.1-68, JET CENTER OF LIFT, 3-D TO 2-D FRACTION
C
         VAR(1)=1./AT
         VAR(2)=0.
         LEN(1)=11
         LEN(2)=11
         CALL INTERX(2,X568,VAR,LEN,Y568,X3D2J,11,121,1,1,0,0,1,1,0,0,
     1               Q568,3,ROUT)
C
C---- SET ALPHA-L TO WING TWIST (WING TWIST ASSUMED TO BE UNIFORM)
C
         AL=TWISTA
         XFICDD=XF2F*X3D2F
         XJICDD=XF2J*X3D2J
C
C---- CALCULATE SECTION PITCHING MOMENT COEFFICIENT
C---- CALCULATE BY SECTION ALONG THE SPAN
C
         VAR(1)=CMUPRM
         VAR(2)=1.0
         LEN(1)=11
         LEN(2)=8
         CALL INTERX(2,X237,VAR,LEN,Y237,XACDD,11,88,1,1,0,0,1,1,0,0,
     1               Q237,3,ROUT)
C
C---- FIG. 6.1.1.1.-49, INCREMENTAL CL-ALPHA
C
         VAR(1)=0.
         VAR(2)=1.
         LEN(1)=9
         LEN(2)=7
         CALL INTERX(2,X149,VAR,LEN,Y149,CLA1,9,63,1,1,0,0,1,1,0,0,
     1               Q149,3,ROUT)
         CLA2=CLA1
         VAR(1)=CMUPRM
         CALL INTERX(2,X149,VAR,LEN,Y149,CLA3,9,63,1,1,0,0,1,1,0,0,
     1               Q149,3,ROUT)
         CLA4=CLA2
         CLA5=CLA4
         DELC41=AL*AK1*CLA1/RAD
         DELC42=AL*AK2*CLA2/RAD
         DELC43=AL*AK3*CLA3/RAD
         DELC44=AL*AK4*CLA4/RAD
         DELC45=AL*AK5*CLA5/RAD
         X21=XMOC
         X22=XMOC
         X23=XMOC-XACDD*CPRIME/C
         X24=XMOC
         X25=XMOC
         DELCM4=-CJ*ETAT*SW/SJ*(XMOC)*AL/RAD
         DCMDA1=DELC41*X21
         DCMDA2=DELC42*X22
         DCMDA3=DELC43*X23+DELCM4
         DCMDA4=DELC44*X24
         DCMDA5=DELC45*X25
         X5=XMOC-XFICDD*CPRIME/C
         VAR(1)=CMUPRM
         VAR(2)=CF/CPRIME
         CALL INTERX(2,X149,VAR,LEN,Y149,CLDFI,9,63,1,1,0,0,1,1,0,0,
     1               Q149,3,ROUT)
         DELC5=DELFLP(N)*AK3*CLDFI/RAD
         DCMDF=DELC5*X5
         VAR(1)=CMUPRM
         VAR(2)=0.
         CALL INTERX(2,X149,VAR,LEN,Y149,CLDJI,9,63,1,1,0,0,1,1,0,0,
     1               Q149,3,ROUT)
         IF(EFFJET(N) .GT. DELFLP(N)) DJ=EFFJET(N)-DELFLP(N)
         IF(EFFJET(N) .LE. DELFLP(N)) DJ=0.0
         IF(JETFLP.LT.5.AND.JETFLP.NE.3.) DJ=DELJET(N)
         DELC6=DJ*AK3*CLDJI/RAD
         X6=XMOC-XJICDD*CPRIME/C
         DCMDJ=DELC6*X6
C
C---- WING PITCHING MOMENT COEFFICIENT
C
         CMM=DCMDJ*BK32+DCMDF*(BK44-BK10)+DCMDA1*BK10+DCMDA2*BK21+
     1       DCMDA3*BK32+DCMDA4*BK43+DCMDA5*BK54
C
C---- NOW GET C(LAMBDA), EQN. 6.1.5.1-T
C
         CL1=BK10*DELC41
         CL2=BK21*DELC42
         CL3=BK32*(DELC43+DELC5+DELC6)
         CL4=BK43*DELC44
         CL5=BK54*DELC45
C
C---- NOW TOTAL WING INCREMENT, EQN. 6.1.5.1-U
C
         DXOCB=(XW+XMOC*CHRDR-XCG)/CBARR
         DELCM(N)=CMM+ETAT*CJ*(ZCG-ZW)/C +DXOCB*
     1            (-CL1-CL2-CL4-CL5-CL3+CJ*ETAT*SW/SJ*AL*SJ/(RAD*SW))
 1090 CONTINUE
 1100 CONTINUE
      RETURN
 1110 FORMAT(55H0*** FLAP IS NOT EXTERNALLY BLOWN, ANALYSIS TERMINATED.)
      END
