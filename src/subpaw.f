      SUBROUTINE SUBPAW
C
C     THIS ROUTINE YIELDS THE FOLLOWING WING DERIVATIVES-
C       SUBSONIC  CLQ
C                 CMQ
C                 CLADOT
C                 CMADOT
C
      COMMON /OVERLY/ NLOG,NMACH,IM,NALPHA
      COMMON /CONSNT/ PI,DR,UNUSED,RAD
      COMMON /OPTION/ SR,CBARR,RUFF,BLREF
      COMMON /WINGD/  A(195)
      COMMON /WINGI/  WINGIN(77)
      COMMON /FLGTCD/ FLC(93)
      COMMON /SYNTSS/ SYNA(19)
      COMMON /POWR/   DYN(213)
      COMMON /IWING/  PW,WING(400)
      COMMON /SBETA/  STB(135),TRA(108)
      COMMON /WHAERO/ C(51)
      COMMON /EXPER/  KEXP(105),KWING
      COMMON /FLOLOG/ FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1                HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,
     2                SUPERS,SUBSON,TRANSN,HYPERS,
     3                SYMFP,ASYFP,TRIMC,TRIM
      LOGICAL FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1        HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,
     2        SUPERS,SUBSON,TRANSN,HYPERS,
     3        SYMFP,ASYFP,TRIMC,TRIM
      LOGICAL KWING
      REAL MACH,LAMDA,MACOE
      DIMENSION ROUTID(2)
      DIMENSION WTYPE(4)
      DIMENSION X21416(8),Y71416(8),Q71416(3)
      DIMENSION X21428(9),Y71428(9)
      DIMENSION I21416(4),I21428(4)
      DIMENSION Q71428(3)
      EQUIVALENCE(WING(101),CLA),(WING(121),CMA),(DYN(21),DCMCLO)
      EQUIVALENCE(COSLC4,A(67)),(TANLC4,A(68)),(ASTRW,A(7))
     1    ,(SW,A(3)),(MACOE,A(16)),(CRSTR,A(10))
     2    ,(LAMDA,A(27)),(DXCG,A(173))
      EQUIVALENCE(CLQWT,WING(201)),(CLADWT,WING(241))
     1     ,(CMQMFB,DYN(1))
      EQUIVALENCE(CLQWSB,WING(201)),(CMQ2,DYN(2)),(CMQWSB,WING(221))
     1    ,(CLG,DYN(4)),(CLADWL,WING(241)),(CMOG,DYN(6)),(CMADPP,DYN(7))
     2    ,(CMADWL,WING(261))
      DATA WTYPE  /4HSTRA,4HDOUB,4HCRAN,4HCURV/
      DATA ROUTID /4HSUBP,4HAW  /
      DATA Q71416 /4H7.1.,4H4.1-,4H6   /
      DATA X21416 /0.,.25,.5,1.5,2.25,2.75,3.25,4./
      DATA Y71416 /0.,.005,.014,.065,.095,.112,.125,.14/
      DATA X11416 /1./
      DATA I21416 /8,3*0/
      DATA I21428 /9,3*0/
      DATA Q71428 /4H7.1.,4H4.2-,4H8   /
      DATA X21428 /0.,.25,.5,1.25,1.75,2.25,2.75,3.25,4./
      DATA Y71428 /0.,.003,.008,.033,.047,.057,.066,.072,.077/
C
      TYPE=WINGIN(15)
      IF(TRANSN.AND.(TYPE.NE.WTYPE(1)))GO TO 1020
      MACH=FLC(IM+2)
      XACCR=C(6)
      DCMDCL=-DCMCLO*CBARR/MACOE
      DCMCLQ=-C(7)*CBARR/MACOE
      IF(TRANSN)DCMCLQ=(TRA(105)*CRSTR-(SYNA(1)-SYNA(2)))/MACOE
      IF(KWING)DCMCLQ=-(CMA/CLA)*CBARR/MACOE
      IF(TRANSN)DCMDCL=(TRA(95)*CBARR-DXCG)/MACOE
      CCLAL=WING(101)
      CLASM=WINGIN(21)*SQRT(ABS(1.-FLC(3)**2))
      IF(TRANSN)CLASM=WINGIN(69)
C
C    ---- WING PITCHING DERIVATIVES,CLQ,CMQ ----
C
      CLQWSB=(.5+2.*DCMCLQ)*CCLAL*MACOE/CBARR
      IF(TRANSN)CLQWT=CLQWSB
      IF(TRANSN)MACH=TRA(6)
      CMQ2=-0.7*CLASM*COSLC4*(ASTRW*(.5*DCMDCL+2.*DCMDCL**2)/(ASTRW+2.*
     1    COSLC4)+ASTRW**3*TANLC4**2/((ASTRW+6.*COSLC4)*24.)+.125)*SW/SR
     2    *(MACOE/CBARR)**2
      IF(MACH.GT.0.2)GO TO 1000
      CMQWSB=CMQ2
      GO TO 1010
 1000 BEE=SQRT(1.-MACH**2*COSLC4**2)
      SAVE=ASTRW**3*TANLC4**2
      CMQWSB=((SAVE/(ASTRW*BEE+6.*COSLC4)+3./BEE)/(SAVE/(ASTRW+6.*COSLC4
     1       )+3.))*CMQ2
      IF(TRANSN)CMQMFB=CMQWSB
      IF(TRANSN)MACH=FLC(IM+2)
      IF(MACH.GT.1.)GO TO 1020
C
C    ---- WING ACCELERATION DERIVATIVES,CLAD,CMAD ----
C
 1010 IF(LAMDA.NE.0.)GO TO 1020
      BETA=SQRT(1.-MACH**2)
      TEST=BETA*ASTRW
      IF(TEST.GT.4.)GO TO 1020
      IF(TRANSN)XACCR=TRA(105)
C
C           -- FIGURE 7.1.4.1-6 --
C
      CALL INTERX(1,X21416,TEST,I21416,Y71416,BPIAO2,8,8,
     1            0,0,0,0,0,0,0,0,Q71416,3,ROUTID)
      CLG=0.
      IF(BETA .NE. 0.)CLG=BPIAO2*PI*ASTRW/(-2.*BETA**2)
      EXPID=SW*MACOE/(SR*CBARR)
      CLADWL=(1.5*XACCR*CCLAL*SR/SW+CLG/19.1  )*EXPID
      IF(TRANSN)CLADWT=CLADWL
C
C           -- FIGURE 7.1.4.2-8 --
C
      CALL INTERX(1,X21428,TEST,I21428,Y71428,BCMPAW,9,9,
     1            0,0,0,0,0,0,0,0,Q71428,3,ROUTID)
      CMOG=0.
      IF(BETA .NE. 0.)CMOG=BCMPAW*ASTRW*0.5*PI/BETA**2
      CMADPP=(-2.53125*XACCR**2*CCLAL*SR/SW+.078532*CMOG)*EXPID
     1       *MACOE/CBARR
      CMADWL=CMADPP+(DXCG/CBARR)*CLADWL
 1020 CONTINUE
      RETURN
      END
