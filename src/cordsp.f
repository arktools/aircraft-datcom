      SUBROUTINE CORDSP
C
C     CALCULATE SUPERSONIC AIRFOIL COORDINATES FROM NACA CARD
C
      COMMON /IWING/  PW,X(60)
      COMMON /IHT/    PHT,XU(60),XL(60),YUU(60),YLL(60)
      COMMON /IVT/    PVT,YU(60),YL(60)
      COMMON /IBW/    PBW,L,I,J,K,II,JJ,KK,III,JJJ,KKK,LLL
      COMMON /WINGI/  WGIN(100)
      COMMON /HTI/    HTIN(154)
      COMMON /VTI/    VTIN(154),TVTIN(8),VFIN(154)
      COMMON /IBWHV/  PBWHV,RHO,TOC
      COMMON /IBODY/  PB,NACA(80)
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD
      REAL KSHARP
      INTEGER A4HH,A4HV,A4HF,A4HSTR
      DATA A4HH,A4HV,A4HF,A4HSTR/4HH   ,4HV   ,4HF   ,4HSTRA/
C
      XT=(100.*J+10.*K+II)/1000.
      TOC=(100.*JJ+10.*KK+III)/1000.
      XF=(100.*JJJ+10.*KKK+LLL)/1000.
      RHO=0.
      GO TO (1020,1000,1050), I
 1000 CONTINUE
C
C     HERE FOR BICONVEX (CIRCULAR ARC)
C
      XT=0.50
      KSHARP=16./3.
      RC=(TOC**2+1.)/(4.*TOC)
      DO 1010 IL=1,60
         XU(IL)=X(IL)
         XL(IL)=X(IL)
         YU(IL)=TOC/2.-RC+SQRT(RC**2-(X(IL)-.5)**2)
         YL(IL)=-YU(IL)
 1010 CONTINUE
      GO TO 1090
 1020 CONTINUE
C
C     HERE FOR DOUBLE WEDGE
C
      KSHARP=(1./XT)/(1.-XT)
      DO 1040 IL=1,60
         XU(IL)=X(IL)
         XL(IL)=X(IL)
         IF(X(IL).GT.XT)GO TO 1030
         YU(IL)=X(IL)*TOC/(2.*XT)
         YL(IL)=-YU(IL)
         GO TO 1040
 1030    YU(IL)=TOC/2.-(X(IL)-XT)*TOC/(2.*(1.-XT))
         YL(IL)=-YU(IL)
 1040 CONTINUE
      GO TO 1090
 1050 CONTINUE
C
C     HERE FOR HEXAGONAL
C
      KSHARP=(1.-XF)/(XT*(1.-XT-XF))
      DO 1080 IL=1,60
         XU(IL)=X(IL)
         XL(IL)=X(IL)
         IF(X(IL).GE.(XT+XF))GO TO 1070
         IF(X(IL).GE.(XT   ))GO TO 1060
         YU(IL)=X(IL)*TOC/(2.*XT)
         YL(IL)=-YU(IL)
         GO TO 1080
 1060    YU(IL)=TOC/2.
         YL(IL)=-YU(IL)
         GO TO 1080
 1070    YU(IL)=TOC/2.-(X(IL)-XT-XF)*TOC/(2.*(1.-XT-XF))
         YL(IL)=-YU(IL)
 1080 CONTINUE
 1090 CONTINUE
C
C     SET SOME OF THE CALCULATED VALUES IN THE INPUT ARRAYS
C
      IF(NACA(6).EQ.A4HH)GO TO 1100
      IF(NACA(6).EQ.A4HV)GO TO 1110
      IF(NACA(6).EQ.A4HF)GO TO 1120
C
C     HERE FOR WING
C
      IF(WGIN(16).EQ.UNUSED)WGIN(16)=TOC
      IF(WGIN(18).EQ.UNUSED)WGIN(18)=XT
      IF(WGIN(70).EQ.UNUSED)WGIN(70)=TOC
      IF(WGIN(71).EQ.UNUSED)WGIN(71)=KSHARP
      IF(WGIN(62).EQ.UNUSED)WGIN(62)=0.
      IF(WGIN(15).NE.A4HSTR.AND.WGIN(63).EQ.UNUSED)WGIN(63)=0.0
      GO TO 1130
 1100 CONTINUE
C
C     HERE FOR HORIZONTAL TAIL
C
      IF(HTIN(16).EQ.UNUSED)HTIN(16)=TOC
      IF(HTIN(18).EQ.UNUSED)HTIN(18)=XT
      IF(HTIN(70).EQ.UNUSED)HTIN(70)=TOC
      IF(HTIN(71).EQ.UNUSED)HTIN(71)=KSHARP
      IF(HTIN(62).EQ.UNUSED)HTIN(62)=0.
      IF(HTIN(15).NE.A4HSTR.AND.HTIN(63).EQ.UNUSED)HTIN(63)=0.0
      GO TO 1130
 1110 CONTINUE
C
C     HERE FOR VERTICAL TAIL
C
      IF(VTIN(16).EQ.UNUSED)VTIN(16)=TOC
      IF(VTIN(18).EQ.UNUSED)VTIN(18)=XT
      IF(VTIN(70).EQ.UNUSED)VTIN(70)=TOC
      IF(VTIN(71).EQ.UNUSED)VTIN(71)=KSHARP
      IF(VTIN(62).EQ.UNUSED)VTIN(62)=0.
      IF(VTIN(15).NE.A4HSTR.AND.VTIN(63).EQ.UNUSED)VTIN(63)=0.0
      GO TO 1130
 1120 CONTINUE
C
C     HERE FOR VENTRAL FIN
C
      IF(VFIN(16).EQ.UNUSED)VFIN(16)=TOC
      IF(VFIN(18).EQ.UNUSED)VFIN(18)=XT
      IF(VFIN(70).EQ.UNUSED)VFIN(70)=TOC
      IF(VFIN(71).EQ.UNUSED)VFIN(71)=KSHARP
      IF(VFIN(62).EQ.UNUSED)VFIN(62)=0.
      IF(VFIN(15).NE.A4HSTR.AND.VFIN(63).EQ.UNUSED)VFIN(63)=0.0
 1130 CONTINUE
      DO 1140 IL=1,60
         YUU(IL)=YU(IL)
         YLL(IL)=YL(IL)
 1140 CONTINUE
C
C     REST OF THE SUPERSONIC OUTPUTS ARE SET IN SECO
C
      RETURN
      END
