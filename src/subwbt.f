      SUBROUTINE SUBWBT
C
C     THIS ROUTINE YIELDS THE FOLLOWING WING-BODY-TAIL DERIVATIVES-
C       SUBSONIC    CYP    (YAW)
C                   CNP    (ROLL)
C                   CNR    (ROLL)
C
      REAL MACH,LP,LPF
      COMMON /OVERLY/ NLOG,NMACH,IM,NALPHA
      COMMON /OPTION/ SREF,CBARR,RUFF,BLREF
      COMMON /FLGTCD/ FLC(93)
      COMMON /SBETA/  STB(135),TRA(108),TRAH(108),STBH(135)
      COMMON /IBWHV/  PBWHV,BWHV(380)
      COMMON /IBWV/   PBWV,BWV(380)
      COMMON /IBWH/   PBWH,BWH(380)
      COMMON /IHT/    PHT,HT(380)
      COMMON /IBW/    PBW,BW(380)
      COMMON /CONSNT/ PI,DR,UNUSED,RAD
      COMMON /IVT/    PVT,VT(380)
      COMMON /IVF/    PVF,VF(380)
      COMMON /FLOLOG/ FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1                HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2                TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3                HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4                VFPL,VFSC,CTAB
      LOGICAL  FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1         HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2         TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3         HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4         VFPL,VFSC,CTAB
      DIMENSION ROUTID(2)
      DIMENSION ALSCHD(20),CYPWBT(20),CYPWBS(20),CNPWBT(20),
     1          CNPWBS(20),CNRWBT(20),CNRWBS(20)
      EQUIVALENCE(CYPWBT(1),BWHV(301)),(CNPWBT(1),BWHV(321)),
     1  (CNRWBT(1),BWHV(341)),(CYPWBS(1),BW(301)),(CNPWBS(1),BW(321)),
     2  (CNRWBS(1),BW(341))
      EQUIVALENCE (ALSCHD(1),FLC(23)),(ZP,STB(12)),(LP,STB(11)),
     1  (DYBV,VT(141)),(ZPF,STBH(12)),(LPF,STBH(11)),(DYBF,VF(141))
      DATA ROUTID/4HSUBW,4HBT  /
      DO 1030 I=1,NALPHA
         COSAL=COS(ALSCHD(I)/RAD)
         SINAL=SIN(ALSCHD(I)/RAD)
         IF(.NOT. VTPL) GO TO 1010
         ZEE=ZP*COSAL-LP*SINAL
         VT(I+300)=2.*(ZEE-ZP)*DYBV/BLREF
         ABCDE=LP*COSAL+ZP*SINAL
         VT(I+320)=-(ABCDE*2.*(ZEE-ZP))*DYBV/BLREF**2
         VT(I+340)=2.* ABCDE**2*DYBV/BLREF**2
 1010    CONTINUE
         IF(.NOT. VFPL)GO TO 1020
         ZEE=ZPF*COSAL-LPF*SINAL
         VF(I+300)=(2.*ZEE-ZPF)*DYBF/BLREF
         ABCDE=LPF*COSAL+ZPF*SINAL
         VF(I+320)=-(2.*ABCDE*(ZEE-ZPF))*DYBF/BLREF**2
         VF(I+340)= 2.*ABCDE**2*DYBV/BLREF**2
 1020    CONTINUE
C
C   GET IOM INCREMENT FOR V.T. TO CYP, CNP AND CNR
C   SET IOM INCREMENT FOR H.T. TO ZERO
C
         BWHV(I+300) = BW(I+300) + VT(I+300) + VF(I+300)
         BWHV(I+320) = BW(I+320) + VT(I+320) + VF(I+320)
         BWHV(I+340) = BW(I+340) + VT(I+340) + VF(I+340)
         HT(I+300) = 0.0
         HT(I+320) = 0.0
         HT(I+340) = 0.0
C
C   ACTUALLY B-W-H-V IS THE SAME AS B-W-V
C
         BWV(I+300) = BWHV(I+300)
         BWV(I+320) = BWHV(I+320)
         BWV(I+340) = BWHV(I+340)
C
C   ACTUALLY B-W-H IS THE SAME AS B-W
C
         BWH(I+300) = BW(I+300)
         BWH(I+320) = BW(I+320)
         BWH(I+340) = BW(I+340)
 1030 CONTINUE
      RETURN
      END
