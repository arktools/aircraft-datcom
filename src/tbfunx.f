      SUBROUTINE TBFUNX(X,Y,DYDX,NP,XA,YA,C,IN,
     1           MI,NG,LEXL,LEXU,MESS,NMSS,ROUT)
C
C**
C** INPUTS
C**
C**    X    = X COORDINATE OF POINT OF INTEREST
C**    NP   = NUMBER OF INPUT DATA POINTS
C**    XA   = ARRAY OF X COORDINATES FOR DATA POINTS
C**    YA   = ARRAY OF Y COORDINATES FOR DATA POINTS
C**    C    = DUMMY ARRAY    -- NOT USED
C**    IN   = DUMMY VARIABLE -- NOT USED
C**    MI   = DUMMY VARIABLE -- NOT USED
C**    NG   = DUMMY VARIABLE -- NOT USED
C**    LEXL = INDICATOR IF BELOW LOWER LIMIT OF DATA
C**           IF .LE. 0, Y IS YA(1) AND DYDX DETERMINED
C**           BY 2ND-ORDER CURVE FIT
C**           IF .EQ. 1, Y IS EXTRAPOLATED LINEARLY USING
C**           SLOPE OF FIRST 2 DATA POINTS
C**           IF .GT. 1, Y AND DYDX ARE DETERMINED BY
C**           2ND-ORDER CURVE FIT
C**    LEXU = INDICATOR IF ABOVE UPPER LIMIT OF DATA
C**           IF .LE. 0, Y IS YA(1) AND DYDX DETERMINED
C**           BY 2ND-ORDER CURVE FIT
C**           IF .EQ. 1, Y IS EXTRAPOLATED LINEARLY USING
C**           SLOPE OF LAST 2 DATA POINTS
C**           IF .GT. 1, Y AND DYDX ARE DETERMINED BY
C**           2ND-ORDER CURVE FIT
C**    MESS = FIGURE NUMBER IN EXTRAPOLATION MESSAGE
C**    NMSS = DIMENSION OF MESS
C**    ROUT = ROUTINE NAME CALLING TBFUNX
C**
C** OUTPUTS
C**
C**    Y    = Y COORDINATE AT POINT OF INTEREST
C**    DYDX = FIRST DERIVATIVE AT POINT OF INTEREST
C**       *          *          *          *          *
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD
      DIMENSION XA(NP),YA(NP),C(6),MESS(2),ROUT(2),MSSCL(9)
      DIMENSION XX(3),YY(3)
      EQUIVALENCE (QSSCL4,MSSCL(4)),(QSSCL6,MSSCL(6))
      DATA MSSCL/4HTBFU, 4HNX  ,2*0,1,4*0/
C**
C**    SET FLAGS FOR USING SUBROUTINE QUAD...
C**
      DATA ADER/4HDERI/,BDER/4HYNOD/
      
C AJT INITIALISE L       
      L=1
      
      NP1=NP-1
      IF(NP.LT.3)GO TO 1070

C**
C**    CHECK IF EXTRAPOLATING OR INTERPOLATING
C**
C AJT      IF(X.GE.XA(NP).OR.X.LE.XA(1))GO TO 1020
C AJT FIND MAX AN MIN VALUES OF XA()
      XAMAX=XA(1)
      XAMIN=XA(1)
      DO 500 I=1,NP
       IF(XA(I).GT. XAMAX) XAMAX=XA(I)
       IF(XA(I).LT. XAMIN) XAMIN=XA(I)
 500  CONTINUE
C AJT      IF(X.GE.XA(NP).OR.X.LE.XA(1))GO TO 1020
C AJT CHECK IF X IS BETWEEN XAMAX AND XAMIN
      IF(X.GE.XAMAX.OR.X.LE.XAMIN)GO TO 1020
C AJT  END OF PATCH
    
C**
C**    HERE IF INTERPOLATING...
C**
C**    DETERMINE POINTS XA(I) SURROUNDING X...
C**
      DO 1000 I=1,NP1
         IF(X.GE.XA(I))L=I
 1000 CONTINUE
      IF(L.EQ.1)L=2
      L2=L-2
C**
C**    FILL VECTORS FOR SUBROUTINE QUAD...
C**
      DO 1010 I=1,3
         XX(I)=XA(L2+I)
 1010 YY(I)=YA(L2+I)
C**
C**    CALCULATE Y BY LINEAR INTERPOLATION...
C**
      DEL=XX(3)-XX(2)
      XDEL=XX(3)
      IF(DEL.EQ.0.)DEL=UNUSED
      Y=YY(2)+(YY(3)-YY(2))*(X-XX(2))/DEL
      DEL=XX(2)-XX(1)
      XDEL=XX(2)
      IF(DEL.EQ.0.)DEL=UNUSED
      IF(X.LT.XA(2))Y=YY(1)+(YY(2)-YY(1))*(X-XX(1))/DEL
C**
C**    SET FLAG FOR CALCULATING DERIVATIVE AND
C**    CALL QUAD TO CALCULATE DYDX...
C**
      YIND=ADER
      CALL QUAD(XX(1),YY(1),X,YIND)
      DYDX=YIND
      GO TO 1050
C**
C**    HERE IF EXTRAPOLATING...
C**
 1020 CONTINUE
      NP3=NP-3
C**
C**    DETERMINE IF ABOVE OR BELOW DATA LIMITS
C**
      LE=0
      LLE=1
      LIND=LEXL
      IF(X.GE.XA(NP))LE=NP3
      IF(X.GE.XA(NP))LLE=NP
      IF(X.GE.XA(NP))LIND=LEXU
C**
C**    FILL VECTORS FOR SUBROUTINE QUAD...
C**
      DO 1030 I=1,3
         XX(I)=XA(LE+I)
 1030 YY(I)=YA(LE+I)
C**
C**    DETERMINE WHICH METHODS FOR CALCULATING Y AND DYDX
C**
C**    IF USING LINEAR EXTRAPOLATION, CALCULATE
C**    Y AND DYDX...
C**
      DEL=XA(2)-XA(1)
      XDEL=XA(2)
      IF(DEL.EQ.0.)DEL=UNUSED
      IF(LEXL.EQ.1)DYDX=(YA(2)-YA(1))/DEL
      DEL=XA(NP)-XA(NP1)
      XDEL=XA(NP)
      IF(DEL.EQ.0.)DEL=UNUSED
      IF(LEXU.EQ.1.AND.LE.EQ.NP3)DYDX=(YA(NP)-YA(NP1))/DEL
      IF(LEXL.EQ.1)Y=(X-XA(1))*DYDX+YA(1)
      IF(LEXU.EQ.1.AND.LE.EQ.NP3)Y=(X-XA(NP))*DYDX+YA(NP)
      IF(LIND.EQ.1)GO TO 1050
C**
C**    HERE FOR RETURNING VALUES AT ENDPOINTS FOR Y...
C**
      IF(LIND.LE.0)Y=YA(LLE)
      IF(LIND.LE.0)GO TO 1040
C**
C**    IF USING SUBROUTINE QUAD, CALCULATE Y...
C**
      YIND=BDER
      CALL QUAD(XX(1),YY(1),X,YIND)
      Y=YIND
C**
C**    IF USING SUBROUTINE QUAD, CALCULATE DYDX...
C**
 1040 YIND=ADER
      CALL QUAD(XX(1),YY(1),X,YIND)
      DYDX=YIND
 1045 CONTINUE
      IF(X.GE.XA(1).AND.X.LE.XA(NP))GO TO 1050
      MSSCL(3)=NMSS
      QSSCL4=Y
      QSSCL6=X
      MSSCL(7)=NP
      MSSCL(8)=LEXL
      MSSCL(9)=LEXU
      CALL MESSGE(ROUT,MESS,XA,MJ,MJ,MJ,MSSCL)
 1050 CONTINUE
      IF(DEL.EQ.UNUSED)WRITE(6,1060)XDEL,ROUT(1),ROUT(2),(MESS(I),
     1                      I=1,NMSS)
 1060 FORMAT(37H WARNING - DUPLICATE X VALUES AT X = ,1PE14.7,
     1 24H IN TBFUNX, CALLED FROM ,2A4,12H FOR FIGURE ,4A4)
      RETURN
 1070 IF(NP.LE.1)Y=YA(1)
      IF(NP.LE.1)DYDX=0.
      IF(NP.LE.1)RETURN
      DEL=0.
      DYDX=(YA(2)-YA(1))/(XA(2)-XA(1))
      Y=YA(1)+DYDX*(X-XA(1))
      GO TO 1045
      END
