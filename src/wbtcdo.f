      SUBROUTINE WBTCDO
C
C *** CALCULATES TRANSONIC WING-BODY-TAIL ZERO LIFT DRAG
C
      COMMON /IBWHV/  PBWT, CDO
      COMMON /FLGTCD/ FLC(95)
      COMMON /WINGI/  WINGIN(100)
      COMMON /BODYI/  BODYIN(126),ITYPE
      COMMON /WINGD/  A(195)
      COMMON /LEVEL2/ SEC(10),CD6,CD7,CD11,CD14,SEC2(8),MD
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD
      COMMON /OVERLY/ NLOG,NMACH,I,NALPHA
C
      EQUIVALENCE (A(7),AR), (WINGIN(16),TOC), (A(27),TAPR),
     1            (A(40),SWEEP)
      REAL M,MD,ITYPE
      DIMENSION PARMA(16),PARMB(20),PARMC(6),PARMD(26),XUNIT(56),
     1   YUNIT(50),ZUNIT(9),TMD(39),Q4519(3),ROUT(2),LENG(4),VAR(4)
      DATA ROUT/4HWBTC,4HDO  /,Q4519/4H4.5.,4H3.1-,4H19  /
      DATA PARMA/0.,10.,20.,30.,40.,50.,60.,0.,.03,.04,.05,.06,.07,.08,
     1 .10,.12/
      DATA PARMB/2.,3.,4.,6.,8.,5*0.,0.,1.,2.,3.,4.,5.,6.,7.,8.,9./
      DATA PARMC/0.,.5,1.,0.,8.,16./
      DATA PARMD/1.,2.,3.,10*0.,0.,1.,2.,3.,4.,5.,6.,7.,8.,9.,10.,11.,
     1  12./
      DATA XUNIT/6.8,7.25,7.75,8.40,9.40,10.8,12.8,5.,5.45,6.05,6.8,7.8,
     19.25,11.15,4.25,4.75,5.3,6.1,7.1,8.7,10.6,3.55,3.9,4.45,5.3,6.5,
     28.1,10.05,3.05,3.4,3.9,4.75,5.85,7.6,9.7,2.5,2.8,3.2,4.,5.2,6.95,
     39.25,1.5,1.8,2.45,3.25,4.5,6.3,8.75,.75,1.15,1.75,2.6,3.85,5.75,
     48.20/
      DATA YUNIT/3.2,2.5,1.95,1.,.4,4.25,3.75,3.,2.1,1.5,5.4,4.8,4.,
     13.25,2.7,6.5,5.9,5.1,4.4,3.8,7.6,7.,6.25,5.55,5.,8.6,8.,7.4,
     26.9,6.3,9.55,9.1,8.7,8.25,7.7,10.45,10.15,9.9,9.65,9.25,5*11.15,
     312.,12.2,12.7,13.,14.25/
      DATA ZUNIT/3*.13,7.78,8.18,8.53,15.43,16.23,16.93/
      DATA TMD/.755,.761,.77,.77,.78,.79,.788,.798,.81,.804,.815,.830,
     1.82,.83,.85,.836,.848,.87,.85,.864,.89,.865,.88,.905,.879,.898,
     2.925,.892,.912,.944,.905,.925,.96,.92,.942,.978,.932,.958,.992/
C
C**** TRANSONIC WING-BODY-TAIL CDO - DATCOM SECTION 4.5.3.1 ************
C**** COMPUTE THE DRAG DIVERGENCE MACH NUMBER, MD
C
C**** WORK ALONG FIGURE 4.5.3.1-19
C**** SWEEP VERSUS THICKNESS RATIO - PART A
C
      M=FLC(I+2)
      VAR(1)=SWEEP
      VAR(2)=TOC
      LENG(1)=7
      LENG(2)=8
      CALL INTERX(2,PARMA,VAR,LENG,XUNIT,X,8,56,1,2,0,0,1,2,0,0,
     1            Q4519,3,ROUT)
C
C**** GO INTO THE ASPECT RATIO PART OF THE FIGURE - PART B
C
      VAR(1)=AR
      VAR(2)=X
      LENG(1)=5
      LENG(2)=10
      CALL INTERX(2,PARMB,VAR,LENG,YUNIT,Y,10,50,1,1,0,0,1,1,0,0,
     1            Q4519,3,ROUT)
C
C**** NOW TAPER RATIO - PART C
C
      VAR(1)=TAPR
      VAR(2)=Y
      LENG(1)=3
      LENG(2)=3
      CALL INTERX(2,PARMC,VAR,LENG,ZUNIT,Z,3,9,1,1,0,0,1,1,0,0,
     1            Q4519,3,ROUT)
C
C**** TO THIS POINT THE SOLUTION IS INDEPENDENT OF GEOMETRY
C**** ITYPE IS THE TYPE OF GEOMETRY TO BE USED FOR THE FINAL SOLUTION
C
      VAR(1)=ITYPE
      VAR(2)=Z
      LENG(1)=3
      LENG(2)=13
      CALL INTERX(2,PARMD,VAR,LENG,TMD,MD,13,39,1,1,0,0,1,1,0,0,
     1            Q4519,3,ROUT)
C
C**** MD NOW CALCULATED, NEED TO CALCULATE CDO AT DESIRED MACH NUMBER
C**** TO COMPLETE THE CALCULATION ... FAIR A CURVE THROUGH THE TRANSONIC
C
      IF(CD11 .NE. UNUSED) GO TO 1000
        IF((CD14 .EQ. UNUSED) .AND. (M .GT. MD)) RETURN
        CD11 = 2.1381*CD14
 1000 CONTINUE
      DCD6=(CD7-CD6)/.1
      DCD11=(CD14-CD11)/.3
      A0=CD7+.002
      A1=0.10
      A2=-.002-.1*(.7-MD)
      IF(M.GT.MD)A2=CD11-A0-.1*(1.1-MD)
      EXPN=((DCD6-.1)*(.7-MD))/(CD7-A0-.1*(.7-MD))
      IF(M.GT.MD)EXPN=((DCD11-.1)*(1.1-MD))/(CD11-A0-.1*(1.1-MD))
      IF(M.LE.MD)CDO=A0+A1*(M-MD)+A2*((M-MD)/(0.7-MD))**(EXPN)
      IF(M.GT.MD)CDO=A0+A1*(M-MD)+A2*((M-MD)/(1.1-MD))**(EXPN)
      RETURN
      END
