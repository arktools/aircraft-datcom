      SUBROUTINE DYNBOD
C
C     THIS ROUTINE YIELDS THE FOLLOWING BODY DERIVATIVES-
C       SUB,TRAN,SUPERSONIC    CLQ
C                              CMQ
C                              CLADOT
C                              CMADOT
C       HYPERSONIC             CNQ
C                              CMQ
C
      REAL LB,LN,LA,MACH,LAMN,LAMA,LAMF,NN,NA,NF
      COMMON /OVERLY/ NLOG,NMACH,IM,NALPHA
      COMMON /OPTION/ SR,CBARR,RUFF,BLREF
      COMMON /CONSNT/ PI,DR,UNUSED,RAD
      COMMON /SYNTSS/ SYNA(19)
      COMMON /BDATA/  BD(762)
      COMMON /IBODY/  PBODY,BODY(400)
      COMMON /BODYI/  BODYIN(126)
      COMMON /SUPBOD/ SBD(227)
      COMMON /POWR/   DYN(213)
      COMMON /FLOLOG/ FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1                HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,
     2                SUPERS,SUBSON,TRANSN,HYPERS,
     3                SYMFP,ASYFP,TRIMC,TRIM
      LOGICAL FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1        HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,
     2        SUPERS,SUBSON,TRANSN,HYPERS,
     3        SYMFP,ASYFP,TRIMC,TRIM
      DIMENSION ROUTID(2)
      DIMENSION F(20),SX(20),X(20)
      EQUIVALENCE (XCG,SYNA(1))
      EQUIVALENCE (X(1),BODYIN(2)),(SX(1),BODYIN(22)),(THETAN,SBD(127)),
     1            (DN,SBD(4)),(D1,SBD(5)),(THETAA,SBD(130)),(D2,SBD(6))
      EQUIVALENCE (CNAN,SBD(128)),(LN,BODYIN(124)),(CNAA,SBD(133)),
     1            (LA,BODYIN(125)),(CNAF,SBD(138)),(CMAF,SBD(139))
      EQUIVALENCE (CMAN,SBD(129)),(CMAA,SBD(134))
      EQUIVALENCE (CLQB,BODY(201)),(CMQB,BODY(221)),(CLADB,BODY(241)),
     1  (CMADB,BODY(261)),(LAMN,DYN(28)),(LAMA,DYN(29)),(LAMF,DYN(30)),
     2  (CNQPN,DYN(31)),(CNQPA,DYN(32)),(CNQPF,DYN(33)),(NN,DYN(34)),
     3  (NA,DYN(35)),(NF,DYN(36)),(CMQPN,DYN(37)),(CNQHYP,BODY(201)),
     4  (CMQPA,DYN(38)),(CMQPF,DYN(39)),(CMQN,DYN(41)),(CMQA,DYN(42)),
     5  (CMQF,DYN(43)),(CMQHYP,BODY(221))
      DATA ROUTID/4HDYNB,4HOD  /
C
      SB=BD(56)
      IF(TRANSN.AND.SB.EQ.UNUSED)SB=SBD(19)
      IF(SUPERS.OR.HYPERS)SB=SBD(19)
      IF(HYPERS)GO TO 1050
C
C***  METHOD NOT VALID FOR BOATTAILS OR FLARES DERIVATIVES WILL BE
C     COMPUTED FOR NOSE CYLINDER ONLY
C
      IF(BODYIN(123) .EQ. 1. .OR. BODYIN(123) .EQ. 2.)WRITE(6,1140)
      LB=LN+LA
      IF(LN.EQ.UNUSED.AND.LA.EQ.UNUSED) LB=BD(1)
      IF(SUPERS)GO TO 1000
      IF(TRANSN)GO TO 1010
      CLAB=BODY(101)
      CMAB=BODY(121)
      GO TO 1020
 1000 CLAB=SBD(18)
      CMAB=SBD(110)
      GO TO 1020
 1010 CLAB=BODY(101)
      CMAB=BODY(121)
 1020 CONTINUE
      NX=BODYIN(1)+.5
      CALL TRAPZ(SX,NX,X,VB,1)
      DO 1030 I=1,NX
 1030 F(I)=SX(I)*X(I)
      CALL TRAPZ(F,NX,X,TIM,1)
      XC=TIM/VB
 1040 SAVE=XCG/LB
      TEMP=VB/(SB*LB)
      DYN(40)=VB
C
C    ---- PITCHING DERIVATIVES, CLQ, CMQ - SUB,SUP ----
C
      CLQB=2.*CLAB*(1.-SAVE)
      CMQB=2.*CMAB*((1.-SAVE)**2-TEMP*(XC/LB-SAVE))/
     1     (1.-SAVE-TEMP)
C
C    ---- ACCELERATION DERIVATIVES, CLAD, CMAD - SUB,SUP ----
C
      CLADB=2.*CLAB*TEMP
      CMADB=2.*CMAB*(TEMP*(XC/LB-SAVE))/
     1      (1.-SAVE-TEMP)
C
C   CORRECT TO REFERENCE AREA AND LENGTH
C
      SBASE=BD(57)
      CLQB = CLQB*(SBASE/SR)*(LB/CBARR)**2
      CMQB = CMQB*(SBASE/SR)*(LB/CBARR)**2
      CLADB=CLADB*(SBASE/SR)*(LB/CBARR)
      CMADB=CMADB*(SBASE/SR)*(LB/CBARR)**2
      RETURN
 1050 CONTINUE
C
C    ---- HYPERSONIC PITCHING DERIVATIVE,CNQ ----
C***  METHOD NOT VALID FOR BOATTAILS IF BOATTAIL IS PRESENT DERIVATIVE
C     IS CALCUALTED FOR NOSE CENTERBODY ONLY
C
      THETAF=SBD(16)
      IF(THETAF .EQ. UNUSED)THETAF=0.0
      IF(THETAF .LE. 0.0)WRITE(6,1150)
      TANN=TAN(THETAN)
      SINN=SIN(THETAN)
      COSN=COS(THETAN)
      TANA=TAN(THETAA)
      SINA=SIN(THETAA)
      COSA=COS(THETAA)
      TANF=TAN(THETAF)
      SINF=SIN(THETAF)
      COSF=COS(THETAF)
      LAMN=0.
      ABC=PI/(4.*SR*CBARR)
      LAMA=DN/D1
      LAMF=D1/D2
      IF(D2.LT.1.E-10) D2=0.01
C
C     PREVENTS USE OF METHOD WITH BOATTAILS
C
      IF(THETAF .LE. 0.0)LAMF=1.0
C
C   ----FIGURE 7.2.1.1-9A  ----
C
      CNQPN=.66667/TANN*(2.*(1.-LAMN**3)-3.*LAMN*COSN**2*(1.-LAMN**2))
      CNQPN=CNQPN*ABC*DN**3
C
C   ----FIGURE 7.2.1.1-9A  ----
C
      IF(LAMA.EQ.1.)GO TO 1060
      CNQPA=.66667/TANA*(2.*(1.-LAMA**3)-3.*LAMA*COSA**2*(1.-LAMA**2))
      GO TO 1070
 1060 CNQPA=0.
 1070 CONTINUE
      CNQPA=CNQPA*ABC*D1**3
C
C   ----FIGURE 7.2.1.1-9A  ----
C
      IF(LAMF.EQ.1.)GO TO 1080
      CNQPF=.66667/TANF*(2.*(1.-LAMF**3)-3.*LAMF*COSF**2*(1.-LAMF**2))
      GO TO 1090
 1080 CNQPF=0.
 1090 CONTINUE
      CNQPF=CNQPF*ABC*D2**3
      NN=XCG/CBARR
      NA=(XCG-LN)/CBARR
      NF=(XCG-LA)/CBARR
      CNQN=CNQPN-2.*NN*CNAN
      CNQA=CNQPA-2.*NA*CNAA
      CNQF=CNQPF-2.*NF*CNAF
      CNQHYP=CNQN+CNQA+CNQF
C
C    ---- HYPERSONIC PITCHING DERIVATIVE,CMQ ----
C
      AN=6.*LAMN**2*(1.-LAMN**2)
      BN=-8.*LAMN*(1.-LAMN**3)
      CN=3.*(1.-LAMN**4)
      AA=6.*LAMA**2*(1.-LAMA**2)
      BA=-8.*LAMA*(1.-LAMA**3)
      CA=3.*(1.-LAMA**4)
      AF=6.*LAMF**2*(1.-LAMF**2)
      BF=-8.*LAMF*(1.-LAMF**3)
      CF=3.*(1.-LAMF**4)
      ABC=ABC/CBARR
C
C   ---- FIGURE 7.2.1.2-12 ----
C
      CMQPN=-(AN*COSN**4+ BN*COSN**2+CN)/(6.*SINN**2)-LAMN**4/2.
      CMQPN=CMQPN*ABC*DN**4
C
C   ---- FIGURE 7.2.1.2-12 ----
C
      IF(LAMA.EQ.1.)GO TO 1100
      CMQPA=-(AA*COSA**4+BA*COSA**2+CA)/(6.*SINA**2)-LAMA**4/2.
      GO TO 1110
 1100 CMQPA=0.
 1110 CONTINUE
      CMQPA=CMQPA*ABC*D1**4
C
C   ---- FIGURE 7.2.1.2-12 ----
C
      IF(LAMF.EQ.1.)GO TO 1120
      CMQPF=-(AF*COSF**4 +BF*COSF**2+CF)/(6.*SINF**2)-LAMF**4/2.
      GO TO 1130
 1120 CMQPF=0.
 1130 CONTINUE
      CMQPF=CMQPF*ABC*D2**4
      CMQN=CMQPN-2.*NN*CMAN+NN*CNQPN-2.*NN**2*CNAN
      CMQA=CMQPA-2.*NA*CMAA+NA*CNQPA-2.*NA**2*CNAA
      CMQF=CMQPF-2.*NF*CMAF+NF*CNQPF-2.*NF**2*CNAF
      CMQHYP=CMQN+CMQA+CMQF
      RETURN
C
 1140 FORMAT(52H WARNING*** BODY ALONE DYNAMIC DERIVATIVE METHOD VAL,
     1 25HID FOR NOSE CYLINDER ONLY,/12X,21HTAIL EFFECTS IGNORED])
 1150 FORMAT(52H WARNING*** BODY ALONE DYNAMIC DERIVATIVE METHOD NOT,
     132H VALID FOR BODIES WITH BOATTAILS,/,12X,25HBOATTAIL EFFECTS IGNO
     2RED])
      END
