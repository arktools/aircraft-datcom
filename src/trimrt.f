      SUBROUTINE TRIMRT
C
C***  CALCULATES SUBSONIC TRIM WITH WING OR HORIZONTAL TAIL CONTROL
C
      COMMON /OVERLY/ NLOG,NMACH,IZ,NALPHA
      COMMON /FLAPIN/ F(69)
      COMMON /IHT/    PHT, HT(380)
      COMMON /IVT/    PVT, VT(380)
      COMMON /IDWASH/ PDWASH, DWASH(60)
      COMMON /IWING/  PWING, WING(400)
      COMMON /IBODY/  PBODY, BODY(400)
      COMMON /WINGD/  A(195),B(49)
      COMMON /HTDATA/ AHT(195),BHT(49)
      COMMON /POWR/   PW(293),TRM(22)
      COMMON /IBW/    PBW, BW(380)
      COMMON /IBWH/   PBWH, BWH(380)
      COMMON /IBWHV/  PBWHV, BWHV(380)
      COMMON /FLGTCD/ FLC(93)
      COMMON /FLOLOG/ FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1                HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2                TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3                HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4                VFPL,VFSC,CTAB
C
      LOGICAL  FLTC,OPTI,BO,WGPL,WGSC,SYNT,HTPL,HTSC,VTPL,VTSC,
     1         HEAD,PRPOWR,JETPOW,LOASRT,TVTPAN,SUPERS,SUBSON,
     2         TRANSN,HYPERS,SYMFP,ASYFP,TRIMC,TRIM,DAMP,
     3         HYPEF,TRAJET,BUILD,FIRST,DRCONV,PART,
     4         VFPL,VFSC,CTAB
      REAL NTRIM
      DIMENSION UTCL(20),UTCM(20),UTCD(20),ALPHA(20),DCM(10),DCL(10),
     1          DCLMAX(10),DCDMIN(10),CHD(10),CDI(200)
      DIMENSION DELTA(10)
      DIMENSION DCM2(10),DELT(10),ROUTID(2)
      DIMENSION C1(6),C2(6),C3(6),C4(6),C5(6)
      DIMENSION DELTAT(20),DCLT(20),CLMAXT(20),CDMINT(20),CHDT(20),
     1          CDIT(20)
      EQUIVALENCE (UTCL(1),HT(201)),(UTCM(1),HT(241)),(UTCD(1),HT(221))
     1         ,(ALPHA(1),TRM(1)),(DELTAT(1),VT(201)),(DCLT(1),VT(221))
     2       ,(CLMAXT(1),VT(241)),(CDMINT(1),VT(281)),(CHDT(1),VT(321))
     3       ,(CDIT(1),VT(261)),(NTRIM,TRM(21))
      EQUIVALENCE (TSTOP,TRM(22))
      EQUIVALENCE (DELTA(1),F(1))
      EQUIVALENCE (DCM(1),WING(211)),(DCL(1),WING(201))
     1,(DCDMIN(1),WING(231)),(CHD(1),WING(261)),(CDI(1),BODY(201))
     2,(DCLMAX(1),WING(221))
      DATA ROUTID/4HTRIM,4HRT  /
C
      IN=0
      I1=0
      I2=0
      I3=0
      I4=0
C
C    -----TRIM HERE WITH CONTROL DEVICE ON WING OR TAIL
C
      NDELTA=F(16)+.5
      NDELTA = NDELTA - 1
      IF(WGPL.AND.HTPL)GO TO 1030
      IF(BO)GO TO 1010
C
C    -----HERE FOR WING ALONE TRIM
C
      DO 1000 J=1,NALPHA
         UTCL(J)=WING(J+20)
         UTCM(J)=WING(J+40)
 1000 UTCD(J)=WING(J)
      ACLMAX=B(43)
      GO TO 1050
C
C    -----HERE FOR WING-BODY TRIM
C
 1010 DO 1020 J=1,NALPHA
         UTCL(J)=BW(J+20)
         UTCM(J)=BW(J+40)
 1020 UTCD(J)=BW(J)
      ACLMAX=B(43)
      GO TO 1050
C
C    -----HERE FOR WING-BODY-TAIL TRIM
C
 1030 DO 1040 J=1,NALPHA
         UTCL(J)=BWH(J+20)
         UTCM(J)=BWH(J+40)
         UTCD(J)=BWH(J)
         IF(VTPL .OR. VFPL) UTCD(J)=BWHV(J)
 1040 CONTINUE
      ACLMAX=BHT(43)
 1050 CONTINUE
      NTRIM=NALPHA
      IF(DCM(1).GT.DCM(NDELTA))GO TO 1070
      DO 1060 J=1,NDELTA
         DELT(J)=DELTA(J)
 1060 DCM2(J)=DCM(J)
      GO TO 1090
 1070 DO 1080 J=1,NDELTA
         N=NDELTA-J+1
         DELT(N)=DELTA(J)
 1080 DCM2(N)=DCM(J)
 1090 DO 1100 J=1,NALPHA
         ALPHA(J)=FLC(J+22)-DWASH(J+20)
 1100 CONTINUE
      DO 1130 J=1,NALPHA
         IF(ALPHA(J).GT.ACLMAX)GO TO 1110
         ARG=-UTCM(J)
         IF(ARG.LT.DCM2(1).OR.ARG.GT.DCM2(NDELTA))GO TO 1120
         CALL TBFUNX(ARG,DELTAT(J),DYDX,NDELTA,DCM2,DELT,C1,IN,MI,NG
     1               ,0,0,4HDELT,1,ROUTID)
         CALL TBFUNX(DELTAT(J),DCLT(J),DYDX,NDELTA,DELTA,DCL,C2,I2,
     1               MI,NG,0,0,4HDCLT,1,ROUTID)
         IF(F(17).LE.5.)CALL TBFUNX(DELTAT(J),CLMAXT(J),DYDX,NDELTA,
     1                  DELTA,DCLMAX,C3,I3,MI,NG,0,0,4HCLMT,1,ROUTID)
         IF(F(17).LE.2.)CALL TBFUNX(DELTAT(J),CDMINT(J),DYDX,NDELTA,
     1                DELTA,DCDMIN,C4,I4,MI,NG,0,0,4HCDMT,1,ROUTID)
         IF(F(17).EQ.1.)CALL TBFUNX(DELTAT(J),CHDT(J),DYDX,NDELTA,DELTA,
     1                  CHD,C5,I5,MI,NG,0,0,4HCHDT,1,ROUTID)
         IF(F(17).LE.6.)CALL TLINEX(DELTA,ALPHA,CDI,NDELTA,NALPHA
     1              ,DELTAT(J),ALPHA(J),CDIT(J),0,0,0,0,4HCDIT,1,ROUTID)
         GO TO 1130
 1110    NTRIM=J-1
         TSTOP=2.
         GO TO 1140
 1120    NTRIM=J-1
         TSTOP=1.
         GO TO 1140
 1130 CONTINUE
 1140 CONTINUE
      RETURN
      END
