      SUBROUTINE WBCLB(NALPHA,CLW,CLAW,CLB,CLAB,CDW,CDB,CLAWB,ALP,
     1    ALPB,WINGIN,XC,TAPR,ALIW,KWB,KBW,CLBN14,CNAM14,CLBLFB,
     2    CLAMFB,M,MFB,KKWB,KKBW,FACT,CLWB,CDWB,CLBB,CLBCL)
C
C *** CALCULATES TRANSONIC WING-BODY ROLLING MOMENT DUE TO YAW,CLB
C
      DIMENSION CLW(20),CLB(20),CDW(20),CDB(20),ALP(20),ALPB(20),
     1          WINGIN(100),CLWB(20),CDWB(20),CLBB(20),FACT(41)
      DIMENSION X12A(11),Y12A1(11),Y12A2(11),C12(6)
      DIMENSION ROUTID(2)
      REAL M,MFB,KWB,KBW,KKWB,KKBW
      LOGICAL FLAG, DRAG
C
      COMMON /CONSNT/ PI,DEG,UNUSED,RAD
C
      DATA X12A  /0., .1, .2, .3, .4, .5, .6, .7, .8, .9, 1./
      DATA Y12A1 /1., .97, .95, 4*.94, .95, .96, .98, .99/
      DATA Y12A2 /0., .11, .21, .31, .41, .51, .60, .70, .80, .90, 1.0/
      DATA ROUTID /4HWBCL, 4HB   /
C
C************ TRANSONIC WING-BODY CL AND CD
C
      YB = WINGIN(4)-WINGIN(3)
      CALL BODOWG(ALPB,XC,YB,WINGIN(4),TAPR,FACT(2),FACT(22),NALPHA)
      FACT(1) = YB/WINGIN(4)
      IN=0
      CALL TBFUNX(FACT(1),KKWB,DYDX,11,X12A,Y12A1,C12,IN,MI,NG,0,0,
     1           4HKKWB,1,ROUTID)
      IN=0
      CALL TBFUNX(FACT(1),KKBW,DYDX,11,X12A,Y12A2,C12,IN,MI,NG,0,0,
     1           4HKKBW,1,ROUTID)
      CLI = CLAW*ALIW
      DRAG = ABS(CDWB(2)) .NE. UNUSED
      DO 1010 J=1,NALPHA
         IF(ABS(CLWB(J)) .NE. UNUSED) GO TO 1000
           CLWB(J) = (CLAB+(KWB+KBW)*CLAW)*ALP(J)
           FLAG = ABS(CLB(J)) .NE. UNUSED .AND. ABS(CLW(J)) .NE. UNUSED
           IF(FLAG) CLWB(J) = CLB(J)+(KWB+KBW)*(CLW(J)-CLI)
           CLWB(J) = CLWB(J)+(KKWB+KKBW)*CLI
           IF(FACT(1) .LT. 1./3.) GO TO 1000
           CLWB(J) = CLWB(J)+FACT(1)*FACT(J+1)*FACT(J+21)*
     1               CLAW*(ALP(J)-ALIW)
 1000    CONTINUE
         IF(DRAG) GO TO 1010
           FLAG = ABS(CDW(J)) .NE. UNUSED .AND. ABS(CDB(J)) .NE. UNUSED
           IF(FLAG) CDWB(J) = CDB(J)+CDW(J)
 1010 CONTINUE
C
C************ TRANSONIC WING-BODY CLB - DATCOM EQUATION 5.2.2.1-D
C
      CLBCL=((CLBN14/CNAM14**2-CLBLFB/CLAMFB**2)*(M-MFB)/(1.4-MFB) +
     1      CLBLFB/CLAMFB**2)*CLAWB**2
      DO 1020 I=1,NALPHA
         IF(CLWB(I).EQ.UNUSED)GO TO 1020
         IF(CLBB(I).EQ.UNUSED)CLBB(I)=CLBCL*CLWB(I)
 1020 CONTINUE
      RETURN
      END
