      SUBROUTINE SPRYAW
C
C    ---THIS SUBROUTINE CALCULATES SUPERSONIC ROLL AND YAW
C     CHARACTERISTICS OF PLAIN T.E. FLAPS (TAPERED AND UNTAPERED),
C     SPOILERS,AND ROLL CHARACTERISTICS OF DIFF DEFLECTED ALL-
C     MOVING HORIZONTAL STABILIZERS.
C
      REAL MACH,IVBH,KHB,KBH,K3,LAMHL,NUGEOM,K1,K2
      LOGICAL TAPERD
      LOGICAL INBORD
      LOGICAL TRANSL
      COMMON /OVERLY/ NLOG,NMACH,IM,NALPHA
      COMMON /OPTION/ SR,CBARR,RUFF,BLREF
      COMMON /FLGTCD/ FLC(93)
      COMMON /WINGI/  WINGIN(77)
      COMMON /WINGD/  A(195)
      COMMON /HTI/    HTIN(131)
      COMMON /CONSNT/ PI,DR,UNUSED,RAD
      COMMON /FLAPIN/ F(69)
      COMMON /IHT/    PHT,HT(380)
      COMMON /IBODY/  PBODY,BODY(400)
      COMMON /IWING/  PWING,WING(400)
      COMMON /POWR/   SPR(59)
      COMMON /WBHCAL/ STP(155)
      COMMON /SUPWH/  STG(141)
      COMMON /HTDATA/ AHT(195),BHT(49)
      DIMENSION ROUTID(2)
      DIMENSION Q22113(3)
      DIMENSION Q22114(3)
      DIMENSION Q21130(3)
      DIMENSION Q212A1(4),Q212A2(4)
      DIMENSION X12A1(11),Y12A1(11),C12A1(6)
      DIMENSION X12A2(11),Y12A2(11),C12A2(6)
      DIMENSION ITRANS(4)
      DIMENSION PASTIM(10)
      DIMENSION CNYWSP(10),CLRLSP(10)
      DIMENSION DELTAS(10)
      DIMENSION TAKTIM(10)
      DIMENSION ALSCHD(20)
      DIMENSION ALSHD(20)
      DIMENSION CLDELB(2)
      DIMENSION TEMPO(2)
      DIMENSION CNYWAL(200)
      DIMENSION DELTAL(10),DELTAR(10)
      DIMENSION CLRLAL(10),CLRLHT(200),CLRLS(200),IVBH(20),GAMMA(20)
      DIMENSION CFACT(10)
      DIMENSION X114A1(3),X114A2(2),Y2114A(6)
      DIMENSION X114E1(1),X114E2(7),Y2114E(7)
      DIMENSION X114F1(1),X114F2(8),Y2114F(8)
      DIMENSION X11301(2),X11302(2),Y21130(4)
      DIMENSION X113A1(4),X113A2(2),Y2113A(8)
      DIMENSION X113B1(4),X113B2(2),Y2113B(8)
      DIMENSION X113C1(4),X113C2(2),Y2113C(8)
      DIMENSION X113E1(2),X113E2(2),Y2113E(4)
      EQUIVALENCE (BSTRO2,WINGIN(2))
      EQUIVALENCE (TAPRI,A(26)),(TAPRO,A(28))
      EQUIVALENCE (ALOCI,F(14)),(ALOCO,F(15))
      EQUIVALENCE (SSPN,WINGIN(4))
      EQUIVALENCE (BETA,SPR(1)),(C1,SPR(2)),(C2,SPR(3)),(LAMHL,SPR(4))
      EQUIVALENCE (PHITE,SPR(5)),(K3,SPR(6)),(SF,SPR(7)),(CLRLF,SPR(8))
      EQUIVALENCE (CLRLAL(1),CLRLSP(1),HT(211))
      EQUIVALENCE (CNYWSP(1),HT(221))
      EQUIVALENCE (CNYWAL(1),BODY(201)),(CLRLHT(1),WING(201))
      EQUIVALENCE (YHS,SPR(11)),(BCLD1 ,SPR(12)),(BCLD2,SPR(13))
      EQUIVALENCE (CFO,F(13)),(CFI,F(12))
      EQUIVALENCE (IVBH(1),STP(133)),(GAMMA(1),STP(111))
      EQUIVALENCE (KHB,SPR(9)),(KBH,SPR(10))
      EQUIVALENCE (CNAHS,HT(101)),(SH,AHT(3)),(SSPNEH,HTIN(3)),
     1            (SSPNH,HTIN(4)),(DELTAL(1),F(19)),(DELTAR(1),F(29))
      EQUIVALENCE (SAI100,A(76))
      EQUIVALENCE (SAI050,A(70))
      EQUIVALENCE (DELTAS(1),F(39))
      EQUIVALENCE (SSPNE,WINGIN(3))
      EQUIVALENCE (CLADEG,WING(101)),(SW,A(3))
      EQUIVALENCE (ALSCHD(1),FLC(23))
      EQUIVALENCE (TANLE,A(62)),(TANTE,A(77))
      EQUIVALENCE (TANHL,SPR(14))
      EQUIVALENCE (TLEOB,SPR(32)),(THLOB,SPR(33)),(TTEOB,SPR(34))
      DATA ITRANS/2,3,4,7/
C
C     ----FIGURE 6.2.2.1-13
C
      DATA X113A2 /0.,.01/
      DATA X113A1 /0.,5.,10.,15./
      DATA Y2113A /-.02,-.02,-.02,-.0176,-.02,-.01068,-.02,-.0004/
      DATA X113B2 /0.,.01/
      DATA X113B1 /0.,5.,10.,15./
      DATA Y2113B /0.,0.,0.,1.2,0.,5.15,0.,11.2/
      DATA X113C2 /0.,.06/
      DATA X113C1 /0.,2.,4.,6./
      DATA Y2113C /0.,0.,0.,1.65,0.,3.38,0.,7.65/
      DATA X113E2 /0.,.8/
      DATA X113E1 /0.,45./
      DATA Y2113E /0.,.00795,0.,.00474/
C
C     ----FIGURE 6.2.1.1-30
C
      DATA X114F2 /3.2,2.6,2.2,2.,1.8,1.7,1.6,1.4/
      DATA X114F1 /1./
      DATA Y2114F /2.15,1.9,1.85,2.,2.25,2.5,2.75,4./
      DATA X11302 /0.,1.9/
      DATA X11301 /0.,45./
      DATA Y21130 /0.,.02,0.,.0184/
C
C     FIGURE 6.2.2.1-14
C
      DATA X114A2 /.4,1./
      DATA X114A1 /0.,.5,1./
      DATA Y2114A /8.7,5.5,6.65,3*5.5/
      DATA X114E2 /3.2,2.8,2.4,2.2,2.0,1.8,1.6/
      DATA X114E1 /1./
      DATA Y2114E /.35,.75,1.35,1.75,2.25,2.75,3.5/
C
C*********          FIGURE 4.3.1.2-12-A1          **********************
C
      DATA X12A1 /0.0,0.1,0.2,0.3,0.4,0.5,0.6,0.7,0.8,0.9,1.0/
      DATA Y12A1 /1.0,0.97,0.95,4*0.94,0.95,0.96,0.98,0.99/
      DATA I12A1 /0/
      DATA Q212A1 /4H4.3.,4H1.2-,4H12-A,4H1   /
C
C*********          FIGURE 4.3.1.2-12-A2          **********************
C
      DATA X12A2 /0.0,0.1,0.2,0.3,0.4,0.5,0.6,0.7,0.8,0.9,1.0/
      DATA Y12A2 /0.0,0.11,0.21,0.31,0.41,0.51,0.60,0.70,0.80,0.90,1.0/
      DATA I12A2 /0/
      DATA Q22113 /4H6.2.,4H2.1-,4H13  /
      DATA Q21130 /4H6.2.,4H1.1-,4H30  /
      DATA Q22114 /4H6.2.,4H2.1-,4H14  /
      DATA Q212A2 /4H4.3.,4H1.2-,4H12-A,4H2   /
      DATA ROUTID /4HSPRY,4HAW  /
C
      STYPE=F(18)
      IF(STYPE.EQ.3.)RETURN
      TRANSL=.FALSE.
      INBORD=.TRUE.
      IF((ALOCO/SSPNE).LT.0.99)INBORD=.FALSE.
      IFTYPE=F(17)+.5
      MACH=FLC(IM+2)
      NDELTA=F(16)+.5
      BETA=SQRT(MACH**2-1.)
      IF(STYPE.EQ.5.)GO TO 1070
      C1=2./BETA
      C2=(2.4*MACH**4-4.*BETA**2)/(2.*BETA**4)
      IF(ALOCI.GE.(SSPN-BSTRO2))TRTOE=TAPRO
      IF(ALOCI.LT.(SSPN-BSTRO2))TRTOE=TAPRI
      LAMHL=ATAN(((ALOCO-ALOCI)*TAN(SAI100/RAD)+CFI-CFO)/(ALOCO-ALOCI))
      TANHL=TAN(LAMHL)
      PHITE=2.*ATAN (F(11)/COS(LAMHL))*RAD
      K3=1.-(C2/C1)*(PHITE/RAD)
      SF=2.*(((CFO+CFI)/2.)*(ALOCO-ALOCI))
      AMGCLN=1.
      SPNSPO=(ALOCO-ALOCI)/SSPNE
      GEOM=((ALOCI+ALOCO)/SSPNE)
      BEF=2.*(ALOCO-ALOCI)
      TLEOB=TANLE/BETA
      THLOB=TANHL/BETA
      TTEOB=TANTE/BETA
C
C   ----RETURN IF L.E. OR T.E. OR H.L. IS SUBSONIC
C
      IF(STYPE.NE.4.)GO TO 1040
      IF(THLOB.GE.1.)RETURN
      IF(TTEOB.GE.1.)RETURN
      TRTOFL=CFO/CFI
      IF(TRTOFL.EQ.1.)GO TO 1000
      AFORLF=TRTOFL
      TAPERD=.TRUE.
      GO TO 1010
 1000 AFORLF=((2.*(ALOCO-ALOCI))**2)/SF*BETA
      TAPERD=.FALSE.
 1010 CONTINUE
      CALL DFLCON(THLOB,TTEOB,AFORLF,BETA,INBORD,TAPERD)
C
C     SUPERSONIC ROLLING EFFECTIVENESS OF TAPERED AND UNTAPERED
C     TRAILING EDGE FLAPS
C
      CLRLF=K3*(SF/SR)*(BCLD1/BETA)*.5*((ALOCI/BLREF)+(BEF/(2.*BLREF))*(
     1      BCLD1/BCLD2))
C
C     SUPERSONIC ROLLING MOMENT COEFFICIENT OF PLAIN
C     TRAILING EDGE CONTROLS
C
      DO 1020 I=1,NDELTA
         CLRLAL(I)=CLRLF*(DELTAL(I)-DELTAR(I))/2.
 1020 CONTINUE
C
C     ----COMPUTE YAWING MOMENT DUE TO AILERON DEFLECTION AT SUPERSONIC
C     SPEEDS
C
      SCALE=SW*2.*SSPNE/(SR*BLREF)
      CLDG=CLADEG
      CLRL=CLRLF
      IF(CLDG.LT.0.0)CLDG=-CLDG
      IF(CLRL.LT.0.0)CLRL=-CLRL
      DO 1030 M=1,NALPHA
         ALSHD(M)=ALSCHD(M)
         IF(ALSHD(M).LT.0.0)ALSHD(M)=-ALSHD(M)
         CALL TLINEX(X113C1,X113C2,Y2113C,4,2,ALSHD(M),CLDG,DEF,0,0,2,1,
     1               Q22113,3,ROUTID)
         DO 1030 J=1,NDELTA
            I=(M-1)*10+J
            DO 1030 K=1,2
               DELFLP=DELTAL(J)
               IF(K.EQ.2)DELFLP=DELTAR(J)
               IF(DELFLP.LT.0.0)DELFLP=-DELFLP
               CALL TLINEX(X113A1,X113A2,Y2113A,4,2,DELFLP,CLRL,
     1                     CLDELB(1),0,0,2,1,Q22113,3,ROUTID)
               CALL TLINEX(X113B1,X113B2,Y2113B,4,2,DELFLP,CLRL,ABC,0,
     1                     1,2,1,Q22113,3,ROUTID)
               CLDELB(2)=(10.-ABC+CLDELB(1)*500.)/500.
               SUBAX1=DEF+CLDELB(2)*(2.5+DEF*1.5)/(-.005)
               SUBAX2=GEOM*SUBAX1*.1*.5
               CALL TLINEX(X113E1,X113E2,Y2113E,2,2,SAI050,SUBAX2,
     1                     TEMPO(K),0,0,2,1,Q22113,3,ROUTID)
               IF(K.EQ.2)CNYWAL(I)=(TEMPO(2)-TEMPO(1))*SCALE
 1030 CONTINUE
      RETURN
 1040 IF(STYPE.EQ.5.)GO TO 1070
C
C     SPOILER ROLLING MOMENTS AT SUPERSONIC SPEEDS
C
      DO 1050 J=1,NDELTA
         DELSOC=DELTAS(J)
         CALL TLINEX(X114A1,X114A2,Y2114A,3,2,TRTOE,GEOM,VERT,0,1,0,1,
     1               Q21130,3,ROUTID)
         VERT2=DELSOC*SPNSPO
         NUGEOM=GEOM*VERT2*10.
         TRNSGF=VERT*NUGEOM*.5
         CALL TLINEX(X114F1,X114F2,Y2114F,1,8,AMGCLN,MACH,ARBIT,0,1,0,
     1               2,Q21130,3,ROUTID)
         FINALY=TRNSGF*(.2+ARBIT*.2)
         CALL TLINEX(X11301,X11302,Y21130,2,2,SAE025,FINALY,TAKTIM(J),
     1               0,0,1,1,Q21130,3,ROUTID)
         CLRLSP(J)=TAKTIM(J)*SW*2.*SSPNE/(BLREF*SR)
 1050 CONTINUE
C
C     COMPUTE YAWING MOMENT DUE TO SPOILER DEFLECTION AT SUPERSONIC
C     SPEEDS
C
      CALL TLINEX(X114A1,X114A2,Y2114A,3,2,TRTOE,GEOM,VERT,0,1,0,1,Q2211
     1            4,3,ROUTID)
      CALL TLINEX(X114E1,X114E2,Y2114E,1,7,AMGCLN,MACH,ARBIT,0,1,0,1,Q22
     1            114,3,ROUTID)
      DO 1060 J=1,NDELTA
         DELSOC=DELTAS(J)
         VERT2=(DELSOC*SPNSPO)/1.2
         NUGEOM=VERT2*(GEOM+.1)*12.-.1
         TRNSGF=VERT*(.05+.5*NUGEOM)
         FINALY=.2*TRNSGF*(1.+ARBIT)
         PASTIM(J)=FINALY*.005
         CNYWSP(J)=PASTIM(J)*2.*SSPNE*SW/(BLREF*SR)
 1060 CONTINUE
      RETURN
 1070 CONTINUE
C
C     SUPERSONIC ROLLING MOMENT FOR ALL MOVEABLE, DIFFERENTIALLY
C     DEFLECTED HORIZONTAL STABILIZER
C
      RADS=SSPNH-SSPNEH
      RATIO=(SSPNH-SSPNEH)/SSPNH
      YHS=.4*SSPNEH+RADS
 1080 CALL TBFUNX(RATIO,KHB  ,DYDX,11,X12A1,Y12A1,C12A1,I12A1,MI,NG,
     1            0,0,Q212A1,4,ROUTID)
 1090 CALL TBFUNX(RATIO,KBH  ,DYDX,11,X12A2,Y12A2,C12A2,I12A2,MI,NG,
     1            0,0,Q212A2,4,ROUTID)
      DO 1100 J=1,NALPHA
         DO 1100 K=1,NDELTA
            I=10*(J-1)+K
            CLRLS(I)=.006108 *(IVBH(J)*GAMMA(J)*(RADS/SSPNEH)+(KBH+KHB)
     1               )*CNAHS*YHS*SH/(BLREF*SR)
            CLRLHT(I)=CLRLS(I)*(DELTAL(K)-DELTAR(K))
 1100 CONTINUE
      RETURN
      END
